@CARDNAME_2142,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 데모네 바르모니카
	CASE "略称"
		RESULTS = 데모네
	CASE "カテゴリ"
		RESULTS:0 = 바르모니카
ENDSELECT


@CARD_2142(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 2100
	CASE "성별"
		RETURN 牝性
	CASE "分割"
		RETURN 펜듈럼
ENDSELECT
RETURN 0

@CARD_EXPLANATION_2142(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("P_MAGIC")
PRINTL 이 카드명의  ③의 P효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,ライフ変強制誘発)
PRINTL ①：자신이 효과 데미지를 받을 때마다, 이 카드에 향명 카운터를 1개 놓는다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②：상대 필드의 몬스터의 공격력은,
PRINTL 자신 필드의 "바르모니카」 카드에 놓여 있는 향명 카운터의 수× 100 내린다.
CALLFORM CARD_ABLE(3,마함起動)
PRINTL ③：자신의 마법 / 함정 존에 "안젤로 바르모니카」가 존재할 경우에 발동할 수 있다.
PRINTL 자신의 패 / EX덱에서 레벨 4의 "바르모니카」 몬스터 1장을 특수 소환한다.
CALL TEXT_DECORATION("P_MAGIC_2")

PRINTL 이 카드명의  ①②의 몬스터 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：이 카드가 패에 존재하고, 자신의 마법 / 함정 존에 "데모네 바르모니카」가 존재하지 않을 경우에 발동할 수 있다.
PRINTL 이 카드를 자신의 마법 / 함정 존에 앞면 표시로 놓는다.
PRINTL 그 후, 자신의 마법 / 함정 존에 "안젤로 바르모니카」 가 존재하지 않을 경우,
PRINTL 패를 1장 버리는 것으로 덱에서 "안젤로 바르모니카」 1장을 자신의 마법 / 함정 존에 앞면 표시로 놓는다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②：자신의 메인 페이즈에, 자신 묘지에서 "바르모니카」 일반 마법 / 일반 함정 1장을 제외하고 발동할 수 있다.
PRINTL 그 마법 / 함정 카드의 데미지를 받는 쪽의 효과를 적용한다.


@CARDSUMMON_AA_2142


@CARDCAN_2142(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2142)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,2142)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 데모네 바르모니카
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL CARD_DRAW_CHECK(1)
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,2142)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 바르모니카
	효과条件종류:0 = 通常마법
	효과条件종류:1 = 通常함정
	효과条件固名前 = 바르모니카 에레디타레
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ライフ変強制誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF ダメージ受者 != 듀얼리스트
		RETURN 0
	SIF ダメージ増減 != "減少"
		RETURN 0
	SIF ダメージ종류 != "효과"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_3(듀얼리스트,2142)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 안젤로 바르모니카
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲EX덱 = 1
	효과条件カテゴリ = 바르모니카
	효과条件종류 = 효과モン
	효과条件レベル = 4
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_2142(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2142,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		SIF 손패数(듀얼리스트) <= 0
			RETURN 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件名前 = 안젤로 바르모니카
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件名前 = 안젤로 바르모니카
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			RETURN 0
		CALL FULLRESET
		효과条件任意 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件名前 = 안젤로 바르모니카
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시",2141)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2142,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 바르모니카
		효과条件종류:0 = 通常마법
		효과条件종류:1 = 通常함정
		효과条件固名前 = 바르모니카 에레디타레
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT != 0
			選択カード_ID = RESULT:1
			TRYCALLFORM CARDEFFECT_{選択カード_ID}_2(듀얼리스트,종류,"효과処理",場所)
		ENDIF
	ENDIF
ELSEIF 종류 == ライフ変強制誘発
	CALL FULLRESET
	CALL SET_FIELD_INFO(듀얼리스트,"마함",場所,"個Ｃ",1)
	PRINTFORML
	PRINTFORMW 黒の杯が満ちていく（현재의 響鳴카운터{GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所")}개）
	PRINTFORML

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 바르모니카
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = -100 * RESULT:9

	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"상대필드",場所,攻撃力修正,0,2142)
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2142,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲EX덱 = 1
		효과条件カテゴリ = 바르모니카
		효과条件종류 = 효과モン
		효과条件レベル = 4
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

