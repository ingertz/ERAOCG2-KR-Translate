@CARDNAME_61391,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 아로마 블렌드
	CASE "略称"
RESULTS = 아로마 블렌드
	CASE "カテゴリ"
RESULTS:0 = 아로마
ENDSELECT


@CARD_61391(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
ENDSELECT
RETURN 0

@CARD_EXPLANATION_61391(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 손패를 1장 버리고 발동할 수 있다.
PRINTL 패 / 덱에서 "윤택한 바람", "메마른 바람", "은혜로운 바람" 중 어느 한 장을 자신 필드의 마법 & 함정 존에 앞면 표시로 놓는다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신의 패 / 필드의 몬스터를 융합 소재로 제외하고, 식물족의 융합 몬스터 1장을 융합 소환한다.
PRINTL 자신의 LP가 상대보다 많을 경우, 자신의 묘지의 식물족 몬스터를 제외하고 융합 소재로 할 수도 있다.

@CARDCAN_61391(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,61391)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,61391)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	IF 종류 == 마함起動
		SIF 손패数(듀얼리스트) == 0
			RETURN 0
	ELSEIF 종류 == 손패起動
		SIF 손패数(듀얼리스트) <= 1
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件名前:0 = 윤택한 바람
	효과条件名前:1 = 메마른 바람
	효과条件名前:2 = 은혜로운 바람
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK(1)
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,61391)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	CALL RESETAREA_MAT
	素材範囲손패 = 1
	素材範囲필드 = 1
	SIF GETVAR(@"ライフ_%듀얼리스트%") > GETVAR(@"ライフ_%対面者%") 	
		素材範囲묘지 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件종족 = 식물족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_61391(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61391,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
	효과条件名前:0 = 윤택한 바람
	효과条件名前:1 = 메마른 바람
	효과条件名前:2 = 은혜로운 바람
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61391,2)
	IF RESULT == 0
		;마법카드를 묘지送り
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲손패 = 1
		SIF GETVAR(@"ライフ_%듀얼리스트%") > GETVAR(@"ライフ_%対面者%") 
			素材範囲묘지 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件종족 = 식물족
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード, 제외行き)
		ENDIF
	ENDIF
ENDIF
