@CARDNAME_41340,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 마돌체 푸딩세스 쇼코 아 라 모드
	CASE "略称"
RESULTS = 초코 푸딩
	CASE "カテゴリ"
RESULTS:0 = 마돌체
ENDSELECT


@CARD_41340(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2200
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARDSUMMON_41340(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 5
素材条件속성 = 땅속성

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,41340) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_41340(종류)
#DIM DYNAMIC 종류

PRINTL 땅 속성 레벨 5 몬스터 2장
PRINTL 이 카드는 자신 필드의 랭크 4 이하의 "마돌체" 엑시즈 몬스터 위에 겹쳐 엑시즈 소환할 수 있다.
PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①: 자신 묘지의 "마돌체" 카드 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 카드를 덱으로 되돌린다.
CALLFORM CARD_ABLE(0,효과後誘発)
PRINTL ②: 자신 묘지의 "마돌체" 카드가 덱/엑스트라 덱에 더해졌을 때, 이 카드의 엑시즈 소재를 1개 제거하고 발동할 수 있다.
PRINTL 덱에서 "마돌체" 몬스터 1장을 특수 소환한다.


@CARDCAN_41340(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,41340)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,41340)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件レベル = 4
	효과条件カテゴリ = 마돌체
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,41340)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 마돌체
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1	

ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "필드"
		RETURN 0

	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0

	CALL FULLRESET
	효과条件カテゴリ = 마돌체
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과山送")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 마돌체
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_41340(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM 選択カード,2


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,41340)
	IF RESULT == 0
		CALL RESETCONDITION_MAT
		必要素材数 = 1
		素材条件レベル = 4
		素材条件カテゴリ = 마돌체
		素材条件종류 = 엑시즈
		重ねてＸ = 1
		TRYCALLFORM XYZSUMMON(듀얼리스트,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,41340,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 마돌체
	
		IF GETVARS(@"%듀얼리스트%_フィールド名")  ==  "マドル シャト-"
			효과条件行先플래그 = 손패行き
		ELSE
			효과条件行先플래그 = 덱行き
		ENDIF
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,41340,0,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 마돌체
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
