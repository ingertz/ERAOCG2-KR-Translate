@CARDNAME_1144,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 환주의 가희 르프랭
	CASE "略称"
RESULTS = 환주 르프랑
	CASE "カテゴリ"
RESULTS:0 = 환주
ENDSELECT


@CARD_1144(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 800
	CASE "分割"
		RETURN 펜듈럼
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_1144(종류)
#DIM DYNAMIC 종류


CALL TEXT_DECORATION("P_MAGIC")
PRINTL 이 카드명의 P효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ①: 자신 필드의 「환주」 융합 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 덱에서 「환주」 몬스터 1장을 묘지에 보내고, 대상 몬스터의 공격력을 턴 종료시까지,
PRINTL 묘지로 보낸 몬스터의 레벨 ×200 올린다.
CALL TEXT_DECORATION("P_MAGIC_2")

PRINTL 이 카드명의 ①②의 몬스터 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(2,소환時 ,특수소환時)
PRINTL ①: 이 카드가 소환ㆍ특수 소환했을 경우에 발동할 수 있다.
PRINTL 덱에서 「환주의 가희 르프랭」 이외의 「환주」 몬스터 1장을 패에 넣는다.
CALLFORM CARD_ABLE(3,효과後誘発)
PRINTL ②: 이 카드가 EX덱에 존재하는 상태에서, 자신 필드에 「환주」 융합 몬스터가 특수 소환되었을 경우에 발동할 수 있다.
PRINTL 이 카드를 마법 & 함정 존에 앞면 표시로 놓는다.

@CARDCAN_1144(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1144)
SIF RESULT == 1
	RETURN 0
IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF 발동済_1(듀얼리스트,1144)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 환주
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 환주
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,1144)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 환주
	효과条件종류 = モン全般
	효과条件固名前 = 환주의 가희 르프랭
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_3(듀얼리스트,1144)
		RETURN 0
	SIF ゾーン != "EX덱"
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 환주
	효과条件종류 = 융합
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_1144(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 기록カード
#DIM DYNAMIC 攻撃力修正

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1144)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1144,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = モン全般
		효과条件カテゴリ = 환주
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
		기록カード = RESULT:1
		IF RESULT == 1
			CALLFORM CARD_{기록カード}("レベル") 
			攻撃力修正 = RESULT * 200
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件カテゴリ = 환주
			CALL CARD_CHOOSE_BASIC(듀얼리스트, "필드",ゾーン,場所)	
			;CALL STATCHANGE_BASIC(듀얼리스트,"필드",場所,500,500)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一攻",攻撃力修正)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1144,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 환주
		효과条件종류 = モン全般
		효과条件固名前 = 환주의 가희 르프랭
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1144,3)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ENDIF