@CARDNAME_29191,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 세이크리드 셰아트
	CASE "略称"
RESULTS = 사 셰아트
	CASE "カテゴリ"
RESULTS:0 = 세이크리드

ENDSELECT


@CARD_29191(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족 
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 100
	CASE "守備力"
		RETURN 1600
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_29191(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 "세이크리드" 이외의 몬스터가 존재하지 않는 경우에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ②: 1턴에 1번, 이 카드 이외의 자신 필드 / 묘지의 "세이크리드" 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 이 카드는 선택한 몬스터와 같은 레벨이 된다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드가 필드에 존재하는 한, 자신은 "세이크리드" 몬스터밖에 EX덱에서 특수 소환할 수 없다.


@CARDCAN_29191(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,29191)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,29191)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,29191,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件固カテゴリ = 세이크리드
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 세이크리드
	효과条件종류 = レベル持ち
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_29191(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択カードレベル
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29191,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29191,10)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 세이크리드
		효과条件종류 = レベル持ち
		효과条件他所 = 1
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT != 9999
			選択カード_ID = RESULT:99
			CALL CARD_SELECT(듀얼리스트,RESULT)
			IF 参照先（CS） == "필드"
				選択カードレベル = GETVAR(@"%듀얼리스트%_MZ_星:選択カード（CS）")
			ELSE
				TRYCALLFORM CARD_{選択カード_ID},"レベル"
				選択カードレベル = RESULT
			ENDIF
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永星",選択カードレベル,上書きモード)
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL CARD_EX_RESTRICTION(듀얼리스트,"세이크리드")
ENDIF
