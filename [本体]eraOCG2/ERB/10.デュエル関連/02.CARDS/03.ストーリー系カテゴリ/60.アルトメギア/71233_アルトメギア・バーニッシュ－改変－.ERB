@CARDNAME_71233,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 아르토메기아 버니시－개변－
	CASE "略称"
RESULTS = 신예학도-개변-
	CASE "カテゴリ"
RESULTS:0 = 아르토메기아
ENDSELECT


@CARD_71233(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "추가日"
		RETURN 250426
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71233(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 자신의 덱 묘지에서 「신예학도 아르토메기아」 1장을 자신의 필드 존에 앞면 표시로 놓는다.
PRINTL 자신 필드에 「신예학도 아르토메기아」가 존재할 경우,
PRINTL 대신에 덱에서 「아르토메기아 버니시－개변－」 이외의 「아르토메기아」 카드 1장을 패에 추가한다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ②: 자신의 「아르토메기아」 몬스터가 공격 대상으로 선택되었을 때,
PRINTL 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 그 공격을 무효로 한다.
PRINTL 그 후, 패에서 「무구한 자 메디우스」 1장을 특수 소환할 수 있다.


@CARDCAN_71233(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71233)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,71233)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 신예학도아르토메기아
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件固ID = 71233
		효과条件カテゴリ = 아르토메기아
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ELSE
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件名前 = 신예학도아르토메기아
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF 발동済_2(듀얼리스트,71233)
		RETURN 0
	SIF !MATCH(ゾーン,"묘지")
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)}("カテゴリ")
	SIF !MATCH(RESULTS,"아르토메기아")
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_71233(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 行先플래그

IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71233,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件名前 = 신예학도아르토메기아
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件固ID = 71233
			효과条件カテゴリ = 아르토메기아
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ELSE
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과条件名前 = 신예학도아르토메기아
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71233,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL EVADE_ATTACK(듀얼리스트)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件名前 = 무구한 자 메디우스
		효과条件任意 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
