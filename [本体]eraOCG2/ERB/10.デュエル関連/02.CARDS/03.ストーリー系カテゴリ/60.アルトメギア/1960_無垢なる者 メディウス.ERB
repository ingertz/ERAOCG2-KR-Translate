@CARDNAME_1960,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 무구한 자 메디우스
	CASE "略称"
RESULTS = 메디우스
	CASE "カテゴリ"
RESULTS:0 = 메디우스
ENDSELECT


@CARD_1960(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 1800
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250426
ENDSELECT
RETURN 0

@CARD_EXPLANATION_1960(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,소환時,특수소환時)
PRINTL ①: 이 카드가 소환 / 특수 소환했을 경우에 발동할 수 있다.
PRINTL 덱에서 「옥신」 몬스터 1장을 특수 소환한다.
PRINTL 특수 소환하지 않았을 경우, 대신 패에 넣는다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 이 카드가 묘지에 존재할 경우에 발동할 수 있다.
PRINTL 자신의 패 / 필드(앞면 표시)에서 몬스터 1장을 덱으로 되돌리고, 이 카드를 특수 소환한다.
PRINTL 이 효과로 특수 소환한 이 카드는, 필드에서 벗어났을 경우에 제외된다.


@CARDSUMMON_AA_1960


@CARDCAN_1960(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1960)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,1960)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 獄신
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,1960)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,1960,"묘지",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과条件표시 = 10
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_1960(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1960,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 獄신
		효과条件종류 = モン全般
		효과条件任意 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT != 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 獄신
			효과条件종류 = モン全般
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1960,2)
	IF RESULT == 0
		CALL FULLRESET
		SIF モンスター数(듀얼리스트) < 5
			효과範囲손패 = 1
		효과範囲필드 = 1
		효과条件표시 = 10
		효과条件종류 = モン全般
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과추가종류 = 2
			CALL CARD_SP_SUMMON(듀얼리스트,"묘지",場所)
		ENDIF
	ENDIF
ENDIF

