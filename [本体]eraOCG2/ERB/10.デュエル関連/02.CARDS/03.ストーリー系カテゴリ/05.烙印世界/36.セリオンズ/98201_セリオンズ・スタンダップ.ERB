@CARDNAME_98201,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 세리온즈 스탠드업
	CASE "略称"
RESULTS = T 스탠드업
	CASE "カテゴリ"
RESULTS:0 = 세리온즈
ENDSELECT


@CARD_98201(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_98201(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①②의 효과는 1턴에 1번, 어느 하나만 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①: 자신 묘지의 "세리온즈" 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.
PRINTL 그 후, 자신 패/묘지에서 "세리온즈" 몬스터 1장을 고르고, 장착 카드로서 그 몬스터에 장착할 수 있다.
CALLFORM CARD_ABLE(1,묘지起動,フリー誘発)
PRINTL ②: 자신 / 상대 메인 페이즈에 묘지의 이 카드를 제외하고,
PRINTL "세리온즈" 몬스터를 자신 필드 / 묘지에서 1장씩 대상으로 발동할 수 있다.
PRINTL 그 묘지의 몬스터를 장착 카드로서 그 자신 필드의 몬스터에 장착한다.

@CARDCAN_98201(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과制限달성
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,98201)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,98201)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 세리온즈
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	SIF 발동済_1(듀얼리스트,98201)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 세리온즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 세리온즈
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_98201(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,98201,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 세리온즈
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1 && 마함数(듀얼리스트) < 5
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲묘지 = 1
			효과条件カテゴリ = 세리온즈
			효과条件종류 = モン全般
			효과条件任意 = 1
			CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,カード소환先)
		ENDIF
	ENDIF

ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,98201,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 세리온즈
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 세리온즈
			효과条件종류 = モン全般
			효과条件任意 = 1
			CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,選択カード)
		ENDIF
	ENDIF
ENDIF
