@CARDNAME_38106,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 세리온즈 이레귤러
	CASE "略称"
RESULTS = T 이레귤러
	CASE "カテゴリ"
RESULTS:0 = 세리온즈
ENDSELECT


@CARD_38106(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 2800
ENDSELECT

RETURN 0

@CARD_EXPLANATION_38106(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신의 묘지의 「세리온즈」 몬스터를 3체까지 대상으로 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환하고, 대상의 몬스터를 장착 카드로서 이 카드에 장착한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 패에서 「무진기관 아르기로 시스템」 1장을 버리고 발동할 수 있다.
PRINTL 상대 필드의 카드를 모두 파괴한다.
CALLFORM CARD_ABLE(3,마함起動)
PRINTL ③: 이 카드가 마법 & 함정 존에 존재할 경우에 발동할 수 있다.
PRINTL 이 카드를 특수 소환한다.
PRINTL 그 후, 이 카드명 이외의 자신 필드의 몬스터 1장을 장착 카드로서 이 카드에 장착한다.


@CARDCAN_38106(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 발동불가,5
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,38106)
SIF RESULT == 1
	RETURN 0


CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT


IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,38106)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,38106,ゾーン)
	SIF RESULT == 1
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 세리온즈
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 발동誘発
	SIF 발동済_2(듀얼리스트,38100)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0

	CALL FULLRESET
	효과条件名前 = 세리온즈“킹”레귤러스
	CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 세리온즈
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,38101)
		발동불가:0 = 1
	SIF 발동済_2(듀얼리스트,38102)
		발동불가:1 = 1
	SIF 발동済_2(듀얼리스트,38104)
		발동불가:2 = 1
	SIF 발동済_2(듀얼리스트,38105)
		발동불가:3 = 1
	SIF 발동済_2(듀얼리스트,38106)
		발동불가:4 = 1
	
	;ボレア
	IF 발동불가:0 != 1
		SIF メインフェイズ != 1
			발동불가:0 = 1
		SIF ターンプレイヤー != 듀얼리스트
			발동불가:0 = 1
		CALL FULLRESET
		효과条件名前 = 세리온즈 "릴리" 보레아
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT != 1
			발동불가:0 = 1
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 세리온즈
		효과条件종류 = 마함全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			발동불가:0 = 1

	ENDIF	
	;アラシア
	IF 발동불가:1 != 1
		SIF メインフェイズ != 1
			발동불가:1 = 1
		SIF ターンプレイヤー != 듀얼리스트
			발동불가:1 = 1
		SIF 손패数(듀얼리스트) < 1
			발동불가:1 = 1
		CALL FULLRESET
		효과条件名前 = 세리온즈 “엠프리스” 아라시아
		효과条件소환可 = 1
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT != 1
			발동불가:1 = 1
	ENDIF

	;アイン
	IF 발동불가:2 != 1
		SIF メインフェイズ != 1
			발동불가:2 = 1
		SIF ターンプレイヤー != 듀얼리스트
			발동불가:2 = 1
		CALL FULLRESET
		효과条件名前 = 세리온즈 "불즈" 아인
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT != 1
			발동불가:2 = 1
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			발동불가:2 = 1
	ENDIF

	;ファム
	IF 발동불가:3 != 1
		SIF ターンプレイヤー == 듀얼리스트
			발동불가:3 = 1
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			발동불가:3 = 1
		CALL FULLRESET
		효과条件名前 = 세리온즈 "리퍼" 팜
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT != 1
			발동불가:3 = 1
	ENDIF

	;イレギュラ-
	IF 발동불가:4 != 1
		SIF メインフェイズ != 1
			발동불가:4 = 1
		SIF ターンプレイヤー != 듀얼리스트
			발동불가:4 = 1
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			발동불가:4 = 1
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件名前 = 무진기관 아르기로 시스템
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			발동불가:4 = 1
	ENDIF

	SIF 발동불가:0 == 1 && 발동불가:1 == 1 && 발동불가:2 == 1 && 발동불가:3 == 1
		RETURN 0
	
	RETURN 1

ELSEIF 종류 == 마함起動
	SIF 발동済_3(듀얼리스트,38106)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,38106,"마함")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件スーパー종류 = 装備마법
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_38106(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성,5
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%


;各種起動효과
IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38106,1)
	IF RESULT == 0
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)

		REPEAT 3
			CALL FULLRESET
			효과条件カテゴリ = 세리온즈
			효과条件종류 = モン全般
			효과条件任意 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"묘지",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL FULLRESET
				CALL CARD_EQUIP_MONSTER(듀얼리스트,カード소환先,"묘지",選択カード)
			ELSEIF 選択カード == 9999
				BREAK
			ENDIF
			SIF 마함数(듀얼리스트) >= 5
				BREAK
		REND
	ENDIF

ELSEIF 종류 == 발동誘発
	SETCOLOR C_YELLOW
PRINTL 레귤러스의 파트가 빛나기 시작했다!
	RESETCOLOR
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38100,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件カテゴリ = 세리온즈
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0

ELSEIF 종류 == 필드起動
	条件달성:0 = 1
	条件달성:1 = 1
	条件달성:2 = 1
	条件달성:3 = 1
	条件달성:4 = 1

	SIF 발동済_2(듀얼리스트,38101)
		条件달성:0 = 0
	SIF 발동済_2(듀얼리스트,38102)
		条件달성:1 = 0
	SIF 발동済_2(듀얼리스트,38104)
		条件달성:2 = 0
	SIF 발동済_2(듀얼리스트,38105)
		条件달성:3 = 0
	SIF 발동済_2(듀얼리스트,38106)
		条件달성:4 = 0
	
	;ボレア
	IF 条件달성:0 == 1
		SIF メインフェイズ != 1
			条件달성:0 = 0
		SIF ターンプレイヤー != 듀얼리스트
			条件달성:0 = 0
		CALL FULLRESET
		효과条件名前 = 세리온즈 "릴리" 보레아
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT != 1
			条件달성:0 = 0
	ENDIF

	;アラシア
	IF 条件달성:1 == 1
		SIF メインフェイズ != 1
			条件달성:1 = 0
		SIF ターンプレイヤー != 듀얼리스트
			条件달성:1 = 0
		SIF 손패数(듀얼리스트) < 1
			条件달성:1 = 0
		CALL FULLRESET
		효과条件名前 = 세리온즈 “엠프리스” 아라시아
		효과条件소환可 = 1
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
			条件달성:1 = 0
	ENDIF

	;ブルズ
	IF 条件달성:2 == 1
		SIF メインフェイズ != 1
			条件달성:2 = 0
		SIF ターンプレイヤー != 듀얼리스트
			条件달성:2 = 0
		CALL FULLRESET
		효과条件名前 = 세리온즈 "불즈" 아인
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT != 1
			条件달성:2 = 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			条件달성:2 = 0
	ENDIF

	;ファム
	IF 条件달성:3 == 1
		SIF ターンプレイヤー == 듀얼리스트
			条件달성:3 = 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			条件달성:3 = 0
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件カテゴリ = 세리온즈
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			条件달성:3 = 1
	ENDIF

	IF 条件달성:4 == 1
		SIF メインフェイズ != 1
			条件달성:4 = 0
		SIF ターンプレイヤー != 듀얼리스트
			条件달성:4 = 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			条件달성:4 = 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件名前 = 무진기관 아르기로 시스템
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			条件달성:4 = 0
	ENDIF
		
	IF 듀얼리스트 == "자신"
PRINTL 기동시킬 파트를 선택해 주세요
		PRINTL
		SIF 条件달성:0 == 1
PRINTL [0] - 보레아
		SIF 条件달성:1 == 1
PRINTL [1] - 아라시아
		SIF 条件달성:2 == 1
PRINTL [2] - 아인
		SIF 条件달성:3 == 1
PRINTL [3] - 팜
		SIF 条件달성:4 == 1
PRINTL [4] - 이레귤러
		BINPUT
		PRINTL
	ELSE
		IF 条件달성:0 == 1
			;ボレア
			RESULT = 0
		ELSEIF 条件달성:1 == 1
			;アラシア
			RESULT = 1
		ELSEIF 条件달성:2 == 1
			;アイン
			RESULT = 2
		ELSEIF 条件달성:3 == 1
			;ファム
			RESULT = 3
		ELSEIF 条件달성:4 == 1
			RESULT = 4
		ENDIF
	ENDIF

	IF RESULT == 0
		SETCOLOR C_YELLOW
PRINTL 보레아의 파트가 빛나기 시작했다!
		RESETCOLOR
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38101,2)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲필드 = 1
			효과範囲마함 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = 세리온즈
				효과条件종류 = 마함全般
				CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ELSEIF RESULT == 1
		SETCOLOR C_YELLOW
PRINTL 아라시아의 파트가 빛나기 시작했다!
		RESETCOLOR
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38102,2)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲손패 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲마함 = 1
				효과条件カテゴリ = 세리온즈
				효과条件종류 = モン全般
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ELSEIF RESULT == 2
		SETCOLOR C_YELLOW
PRINTL 아인의 파트가 빛나기 시작했다!
		RESETCOLOR
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38104,2)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과条件カテゴリ = 세리온즈
			효과条件行先플래그 = 破壊行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)

			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")
		ENDIF
	ELSEIF RESULT == 3
		SETCOLOR C_YELLOW
PRINTL 팜의 파트가 빛나기 시작했다!
		RESETCOLOR
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38105,2)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲마함 = 1
			효과条件カテゴリ = 세리온즈
			효과条件行先플래그 = 손패行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)

			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"손패")
		ENDIF
	ELSEIF RESULT == 4
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38106,2)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件名前 = 무진기관 아르기로 시스템
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場全体",場所,"묘지")
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38106,3)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"마함",場所)
		
		CALL FULLRESET
		효과条件固名前 = 세리온즈 이레귤러
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			CALL CARD_EQUIP_MONSTER(듀얼리스트,カード소환先,"필드",選択カード)
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件カテゴリ = 세리온즈
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"破耐")
ENDIF



