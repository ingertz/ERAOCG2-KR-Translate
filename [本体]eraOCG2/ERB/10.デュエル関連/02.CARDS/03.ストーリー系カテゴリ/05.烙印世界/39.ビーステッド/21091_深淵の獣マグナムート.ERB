@CARDNAME_21091,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 비스테드 마그나무트
	CASE "略称"
RESULTS = 마그나무트
	CASE "カテゴリ"
RESULTS:0 = 비스테드
ENDSELECT


@CARD_21091(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 雄性
	CASE "レア度"
		RETURN ULR

ENDSELECT
RETURN 0

@CARD_EXPLANATION_21091(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動,フリー誘発)
PRINTL ①: 이 카드가 패에 존재할 경우에 발동할 수 있다.
PRINTL (자신 필드에 "낙인" 지속 마법/지속 함정이 존재할 경우, 이 효과는 상대 턴에도 발동할 수 있다)
PRINTL 자신 묘지의 빛/어둠 속성 몬스터 1장을 제외하고,
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 일반 소환 / 특수 소환되었을 경우에 발동할 수 있다.
PRINTL 이 턴의 엔드 페이즈에, 자신 덱/묘지에서 "비스테드 마그나무트" 이외의 드래곤족 몬스터 1장을 패에 넣는다.


@CARDSUMMON_AA_21091


@CARDCAN_21091(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21091)
SIF RESULT == 1
	RETURN 0


IF 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,21091)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,21091,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件カテゴリ = 낙인
	효과条件종류:0 = 지속마법
	효과条件종류:1 = 지속함정
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1 && 종류 == 손패起動
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0	
		SIF メインフェイズ != 1
			RETURN 0
	ELSEIF RESULT != 1 && 종류 == フリー誘発
		RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성:0 = 빛속성
	효과条件속성:1 = 어둠속성
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,21091)
		RETURN 0

	RETURN 1
ELSEIF 종류 == ターン終了時予約
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件종족 = 드래곤족
	효과条件종류 = モン全般
	효과条件固名前 = 비스테드 마그나무트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_21091(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21091,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件속성:0 = 빛속성
		효과条件속성:1 = 어둠속성
		효과条件종류 = モン全般
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21091,2)
	IF RESULT == 0
		CALL CARD_RESERV(듀얼리스트,21091)
	ENDIF
ELSEIF 종류 == ターン終了時予約
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21091)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件종족 = 드래곤족
		효과条件종류 = モン全般
		효과条件固名前 = 비스테드 마그나무트
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

