@CARDNAME_1884,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 알버스의 낙윤
	CASE "略称"
RESULTS = 알버스
	CASE "カテゴリ"
RESULTS:0 = 알버스의 낙윤
RESULTS:1 = 알버스
ENDSELECT


@CARD_1884(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_1884(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,소환時,특수소환時)
PRINTL ①: 이 카드가 소환•특수 소환되었을 경우 발동할 수 있다.
PRINTL 손패 1장을 묘지로 보내고, 자신 / 상대 필드의 몬스터를 융합 소재로 하여,
PRINTL 융합 몬스터 1장을 EX덱에서 융합 소환한다.
PRINTL 그 경우, 첫 번째 융합 소재는 「알버스의 낙윤」이어야 한다.


@CARDCAN_1884(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1884)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,1884)
		RETURN 0
	SIF 손패数(듀얼리스트) == 0
		RETURN 0
	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲상대필드 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件소환可 = 1
	強制素材場所 = 지정場所(場所,ゾーン)
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_1884(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1884,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)		
		IF RESULT == 1
			CALL RESETAREA_MAT
			素材範囲필드 = 1
			素材範囲상대필드 = 1
			CALL FULLRESET
			효과条件종류 = 융합
			強制素材場所 = 지정場所(場所,ゾーン)
			효과条件소환可 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL FUSSUMMON(듀얼리스트, 選択カード)
			ENDIF
		ENDIF
	ENDIF		
ENDIF
