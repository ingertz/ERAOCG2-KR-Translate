@CARDNAME_81890,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 트라이브리게이드 리볼트
	CASE "略称"
RESULTS = 철수의 항전
	CASE "カテゴリ"
RESULTS:0 = 트라이브리게이드
ENDSELECT


@CARD_81890(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_81890(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 카드는 1턴에 1장밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①: 자신의 묘지의 몬스터 및 제외되어 있는 "트라게" 몬스터를 가능한 한 효과를 무효로 하여 특수 소환한다.
PRINTL 그 후, EX덱의 "트라게" L 몬스터 1장을 선택해도 좋다.
PRINTL 그렇게 했을 경우, 그 링크 몬스터의 링크 수만큼 자신 필드의 "트라게" 몬스터를 묘지로 보내고,
PRINTL 그 "트라게" 링크 몬스터 1장을 링크 소환 취급으로 특수 소환한다.
PRINTL 


@CARDCAN_81890(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 最大링크値
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,81890)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,81890)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件カテゴリ = 트라이브리게이드
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 트라이브리게이드
	효과条件特不無視 = 1
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ENDIF

@CARDEFFECT_81890(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード2
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択링크値
#DIM DYNAMIC 最大링크値
#DIM DYNAMIC 소환カード
#DIM DYNAMIC モンスター数,5
#DIM DYNAMIC SS可能数
#DIM DYNAMIC SSカウント

IF 종류 == 마함起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,81890,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		モンスター数:0 = RESULT

		SS可能数 = 5 - モンスター数:0
		
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 트라이브리게이드
		효과条件종류 = モン全般
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		モンスター数:1 = RESULT

		IF SS可能数 > モンスター数:1
			SS可能数 = モンスター数:1
		ENDIF

		FOR カウンタ,0,SS可能数
			CALL FULLRESET
			효과範囲묘지 = 1
			효과範囲제외 = 1
			효과条件カテゴリ = 트라이브리게이드
			효과条件종류 = モン全般
			효과추가종류 = 効追無効
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"攻撃표시")
		NEXT

		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 트라이브리게이드
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		最大링크値 = RESULT

		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件カテゴリ = 트라이브리게이드
		CALL 효과条件レベル_ADV(最大링크値, "以下")
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
			

		CALL FULLRESET
		효과条件カテゴリ = 트라이브리게이드
		효과条件종류 = 링크
		CALL 효과条件レベル_ADV(最大링크値, "以下")
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "EX덱",ゾーン,場所)	
		소환カード = RESULT

		IF 소환カード != 9999
			CALLFORM CARD_{GETVAR(@"%듀얼리스트%_EX덱:소환カード")}, "レベル"
			選択링크値 = RESULT

			CALL FULLRESET
			효과範囲필드 = 1
			효과条件カテゴリ = 트라이브리게이드
			효과条件複数 =	選択링크値
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_EX_SUMMON(듀얼리스트, "EX덱", 소환カード,"攻撃표시")
			ENDIF
		ENDIF
	ENDIF
ENDIF
