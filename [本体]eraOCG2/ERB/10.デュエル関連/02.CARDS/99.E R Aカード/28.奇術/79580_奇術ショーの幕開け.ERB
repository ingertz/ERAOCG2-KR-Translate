@CARDNAME_79580,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 「마술쇼의 개막」
	CASE "略称"
		RESULTS = 기술개막
	CASE "カテゴリ"
		RESULTS:0 = 기술
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79580(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
	CASE "추가日"
		RETURN 250415
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79580(종류)
#DIM DYNAMIC 종류


CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  ①②의 효과는 １턴에 １번、어느 쪽이든 １개밖에 사용할 수 없으며、
PRINTL 이 효과를 발동하는 턴、자신은 Ｘ몬스터밖에 ＥＸ덱에서 특수 소환할 수 없다。
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：자신의 덱 / 묘지 / 제외 상태에서 "마술쇼의 조수：판도라 매지션 걸" １장을 패에 넣는다。
PRINTL 효과해결 시에 자신 패 / 필드의 몬스터 １장을 릴리스하는 것으로、패에 넣지 않고 특수 소환할 수도 있다。
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다。
PRINTL "「마술쇼의 개막」" 이외의 자신 묘지 / 제외 상태인 "기술" 카드 １장을 골라 덱으로 되돌리고、
PRINTL 자신은 １장 드로우한다。
CALL TEXT_DECORATION("ROGUE")

;ただただ판도라 매지션 걸を出すためだけのカードであり、「죄보狩りの악마」相当のパワーを갖는다
;흑마녀ディアベルスタ-とほぼ同じハンド消費で出せるが、明確な違いとして、
;이 카드는 발동前の段階でＸモンスター縛りがつき、ＰＭＧもＸ縛りがつく点が重い。


@CARDCAN_79580(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79580)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 발동済_1(듀얼리스트,79580)
		RETURN 0
	SIF 발동済_2(듀얼리스트,79580)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件名前 = 마술쇼의 조수：판도라 매지션 걸
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
		
	;엑시즈以外をEXから出していたら효과使用불가能
	CALL FULLRESET
	효과条件종류 = 엑시즈以外
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX이력")
	SIF RESULT != 0
		RETURN 0
	
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,79580)
		RETURN 0
	SIF 발동済_2(듀얼리스트,79580)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_ドロー불가") >= 1
		RETURN 0
	;ドロー효과
	CALL CARD_DRAW_CHECK
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件カテゴリ = 기술
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
		
	;엑시즈以外をEXから出していたら효과使用불가能
	CALL FULLRESET
	효과条件종류 = 엑시즈以外
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX이력")
	SIF RESULT != 0
		RETURN 0
	
	RETURN 1
ENDIF



@CARDEFFECT_79580(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択結果

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79580,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		;엑시즈縛りを付与
		EX소환可能종류 = 엑시즈
		CALL CARD_EX_RESTRICTION(듀얼리스트)
		
		;패에 넣거나 몬스터 １장을 묘지로 보내고 특수 소환하거나 選択する処理
		;CPUの場合は「いいえ」扱いで패에 넣는다효과를 使う（ 몬스터의 リリースはしない）
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲필드 = 1
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT != 1
			;条件を満たしていない場合は選択肢不要で「いいえ」扱いで패에 넣는다処理を行う。
			選択結果 = いいえ
		ELSE
			IF モンスター数(듀얼리스트) >= 5
				;필드의 몬스터가 塞がっている場合「いいえ」扱いにする（패에 넣는다だけ）
				;本当は필드의 몬스터를 릴리스하고 空ければ특수소환できるけど処理をちょっと省略
				選択結果 = いいえ
			ELSEIF 듀얼리스트 == "상대"
				;CPUの場合は選択肢を省略して「いいえ」扱いにする（패에 넣는다）
				選択結果 = いいえ
			ELSE
				;条件を満たしている場合は패 / 필드의 몬스터를 릴리스하고 특수 소환하거나 選択
				PRINTL 패 / 필드의 몬스터 １장을 릴리스 하고、
				PRINTL "마술쇼의 조수：판도라 매지션 걸"을 특수 소환하시겠습니까？
				CALL YN_CHOICE(듀얼리스트,"●몬스터 １장을 릴리스하고 특수 소환한다","●패에 넣는다")
				選択結果 = RESULT
			ENDIF
		ENDIF
		
		IF 選択結果 == はい
			;패 / 필드의 몬스터 １장을 リリースする
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲필드 = 1
			효과条件종류 = モン全般
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				;특수소환
				;CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
				CALL FULLRESET
				효과範囲덱 = 1
				효과範囲묘지 = 1
				효과範囲제외 = 1
				효과条件名前 = 마술쇼의 조수：판도라 매지션 걸
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ELSE
			;패에 넣는다
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과範囲제외 = 1
			효과条件名前 = 마술쇼의 조수：판도라 매지션 걸
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79580,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		;엑시즈縛りを付与
		EX소환可能종류 = 엑시즈
		CALL CARD_EX_RESTRICTION(듀얼리스트)
		
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 기술
		효과条件固名前 = 「마술쇼의 개막」
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL CARDDRAW_BASIC(듀얼리스트,1)
		ENDIF
	ENDIF
ENDIF

