@CARDNAME_79625,参照先
;ココで지정卡片の名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 바르모니카 레타 라모아
	CASE "略称"
		RESULTS = L’amore
	CASE "カテゴリ"
		RESULTS:0 = 바르모니카
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_79625(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
ENDSELECT

RETURN 0


@CARD_EXPLANATION_79625(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의  카드는 1턴에 1장 밖에 사용하지 못하고,
PRINTL 상대는 이 카드의 발동에 대해서 효과를 발동할 수 없다.
CALL CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：자신의 패 / 덱 / 묘지/제외 존의「셀레트리체 바르모니카」1장을 특수 소환한다.
PRINTL 이 효과로 특수 소환한 몬스터는 다른 카드의 효과를 받지 않는다.
PRINTL 자신의 필드에「셀레트리체 바르모니카」가 존재할 경우,
PRINTL 대신에「바르모니카 러브레터」이외의「바르모니카」카드 1장을 패로 가져온다.
CALL CARD_ABLE(2,발동誘発)
PRINTL ②：자신의 필드에「바르모니카」카드가 존재할 때,
PRINTL 상대가「E.R.A」카드의 효과를 발동 했을 때에 발동할 수 있다.
PRINTL 묘지의 이 카드를 제외하고, 그 발동을 무효로 하고, 파괴한다.


@CARDCAN_79625(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 종류
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79625)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,79625)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	;자신 필드의 チェック
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件名前 = 셀레트리체 바르모니카
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		;存在しない時
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件名前 = 셀레트리체 바르모니카
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ELSE
		;존재하는 時
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 바르모니카
		효과条件固名前 = 바르모니카 러브레터
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
		
	RETURN 1

ELSEIF 종류 == 발동誘発
	SIF ゾーン != "묘지"
		RETURN 0
	;제외が可能かの確認
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 바르모니카
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과条件カテゴリ = E.R.A
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == チェーン可否
	RETURN 1
ENDIF



@CARDEFFECT_79625(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 종류
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	;卡片发动確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79625,1)
	IF RESULT == 0
		;通常마법汎用処理
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)

		CALL FULLRESET
		효과範囲필드 = 1
		효과条件名前 = 셀레트리체 바르모니카
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과範囲제외 = 1
			효과条件カテゴリ = 바르모니카
			효과条件固名前 = 바르모니카 러브레터
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ELSE
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲손패 = 1
			효과範囲묘지 = 1
			효과範囲제외 = 1
			효과추가종류:0 = 54
			효과추가종류:1 = 55
			효과추가종류:2 = 56
			효과条件名前 = 셀레트리체 바르모니카
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)

			;상대가 使った時に流れると不具合扱いされそうなので자신이 使用した경우에 만 
			IF 듀얼리스트 == "자신"
				SETCOLOR C_PINK
				PRINTFORML
				PRINTFORML 「당신이 절 불러준다면, 저는 어디에 있더라도 당신을 만나러 갈거예요.」
				PRINTFORML "편지"에 쓰여진 마음이 %CALLNAME:NO_TO_CHARANUM(2140)%를 지키고 있다!
				PRINTFORMW
				RESETCOLOR
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79625,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"제외")

		SETCOLOR C_PINK
		PRINTFORML
		PRINTFORML "편지"에 담겨진 마음이「E.R.A」카드를 연주했다!
		PRINTFORMW
		RESETCOLOR
		RETURN 1
	ENDIF
	RETURN 0
ENDIF

