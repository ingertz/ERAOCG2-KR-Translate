@CARDNAME_4805,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 기계의 마니
	CASE "略称"
RESULTS = 마니
	CASE "カテゴリ"
RESULTS:0 = 일바의 신
RESULTS:2 = 엘로나
ENDSELECT


@CARD_4805(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 엑시즈
	CASE "속성"
		RESULT:1 = 어둠속성
		RETURN 신속성
	CASE "종족"
		RETURN 환신야수족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 3000
	CASE "성별"
		RETURN 雄性
	CASE "특수불가"
		RETURN 1
	CASE "C불가"
		RETURN 1
ENDSELECT
RETURN 0

@CARDSUMMON_4805(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照


CALL RESETCONDITION_MAT
必要素材数 = 5
素材条件レベル = 10

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,4805) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_4805(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 10 몬스터 ×5
PRINTL
PRINTL 이 카드의 효과 발동에 대해 상대는 효과를 발동할 수 없다.
PRINTL 이 카드는 엑스 소환으로만 특수 소환할 수 있다.
CALL CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ①: 자신 또는 상대 턴에 1번, 이 카드의 엑스 소재를 2개 제거하고 발동할 수 있다.
PRINTL 필드의 카드를 1장 파괴하고, 자신 또는 상대 필드에 '안드로이드'를 2체까지 생산한다.
CALL CARD_ABLE(0,ターン終了時強制)
PRINTL ②: 엔드 페이즈에 발동한다.
PRINTL 이 카드의 엑스 소재를 1개 늘린다.
CALL CARD_ABLE(0,지속효과)
PRINTL ③: 상대 필드의 기계족 몬스터는 공격할 수 없으며, 소재 불가를 얻는다.
PRINTL 또한, 상대 필드에 기계족 몬스터만 존재할 경우, 자신 필드의 모든 몬스터는 상대에게 직접 공격할 수 있다.


@CARDCAN_4805(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4805)
SIF RESULT == 1
	RETURN 0

IF 종류 == ターン終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") < 1
		RETURN 0
	IF 듀얼리스트 != "자신"
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4805(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 발동불가

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == ターン終了時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4805)
	IF RESULT == 0
		SETCOLOR C_XYZ_BLACK
		PRINTW 「焦るな。　すぐに機械が全てを支配する時代が来る。」
		RESETCOLOR
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"Ｘ",1)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4805,10,X消費2)
	IF RESULT == 0
		SETCOLOR C_XYZ_BLACK
		PRINTW 「機械による統治こそ人間の救いだ。」
		RESETCOLOR
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"両全場単体",場所,"묘지")
		IF RESULT == 1
			REPEAT 2
				SIF 듀얼리스트 == "자신"
PRINTL 생산하는 마니?
				CALL YN_CHOICE(듀얼리스트)
				SIF RESULT == 1
					BREAK
				
				IF モンスター数(듀얼리스트) < 5 && モンスター数(対面者) < 5
					SIF 듀얼리스트 == "자신"
PRINTL 어느 쪽에 사출하는 마니?
					CALL YN_CHOICE(듀얼리스트,"자신 필드","상대 필드")
				ELSEIF モンスター数(듀얼리스트) < 5
					RESULT = 1
				ELSEIF モンスター数(対面者) < 5
					RESULT = 0
				ENDIF

				IF RESULT == 0
					CALL FULLRESET
					CALL CARD_GENERATE(듀얼리스트,14805)
				ELSEIF RESULT == 1
					CALL FULLRESET
					CALL CARD_GENERATE(対面者,14805)
				ENDIF
			REND
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件종족 = 기계족
	CALL STATCHANGE_EFFECT(듀얼리스트,"상대필드",場所,"戦無")
	CALL STATCHANGE_EFFECT(듀얼리스트,"상대필드",場所,"材不")

	FOR カウンタ,1,5
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件종족 = カウンタ
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			발동불가 ++
	NEXT
	FOR カウンタ,6,21
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件종족 = カウンタ
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			발동불가 ++
	NEXT
	IF 발동불가 == 0
		CALL FULLRESET
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"直攻")
	ENDIF
ENDIF



@CARDNAME_14805,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 안드로이드 (마니 산)
	CASE "略称"
RESULTS = 마니 산 안드로이드
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_14805(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 1000
ENDSELECT

@CARD_EXPLANATION_14805(종류)
#DIM DYNAMIC 종류

PRINTL 그것은 '기계의 마니'에 의해 만들어졌다.
CALL CARD_ABLE(0,バトル終了時強制)
PRINTL ①: 이 카드가 전투 가능할 경우, 자신 배틀 페이즈 종료 시에 발동한다.
PRINTL 이 카드의 컨트롤러에게 500 데미지를 입힌다.
PRINTL

@CARDCAN_14805(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,14805)
SIF RESULT == 1
	RETURN 0

IF 종류 == バトル終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_戦:場所") == 0
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_14805(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

IF 종류 == バトル終了時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,14805)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",500)
	ENDIF
ENDIF
