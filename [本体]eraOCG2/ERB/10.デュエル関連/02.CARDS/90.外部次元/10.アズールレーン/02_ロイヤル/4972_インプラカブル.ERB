@CARDNAME_4972,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 항공모함 임플라커블
	CASE "略称"
RESULTS = 임플라커블
	CASE "カテゴリ"
RESULTS:0 = 벽람항로
RESULTS:1 = 로열(AL)
ENDSELECT

@CARD_4972(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 9
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 2800
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_4972(종류)
#DIM DYNAMIC 종류

PRINTL "로열(AL)" 튜너 + 튜너 이외의 "로열(AL)" 몬스터 1장 이상
PRINTL 이 카드명의 ②③의 효과는 각각 1턴에 1번만 사용할 수 있다.
CALL CARD_ABLE(0,효과後強制誘発)
PRINTL ①: 자신 필드의 "로열(AL)" 몬스터가 패 / 필드 / 묘지에서 덱 / 패로 되돌아갈 때마다 발동한다.
PRINTL 이 카드의 공격력을 300 올린다.
CALL CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ②: 자신 / 상대 턴에 이 카드의 공격력이 2000 이상이라면, 이 카드 이외의 필드의 카드 1장을 대상으로 발동할 수 있다.
PRINTL 이 카드의 공격력을 600 내리고, 그 카드를 패로 되돌린다.
CALL CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ③: 자신 / 상대 턴에, 자신 필드의 이 카드 이외의 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터를 덱으로 되돌리고, 덱에서 대상 몬스터의 레벨보다 레벨이 낮은 "로열(AL)" 몬스터 1장을 특수 소환한다.

@CARDSUMMON_4972(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要レベル数 = 9
素材条件カテゴリ = 로열(AL)

;1体目の条件
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,4972) 
	RETURN RESULT
ENDIF

@CARDCAN_4972(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4972)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 로열(AL)
	효과条件종류 = モン全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"손패덱")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	IF !발동済_1(듀얼리스트,4972)
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		효과条件他所 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1 && GETVAR(@"%듀얼리스트%_MZ_攻:場所") >= 2000
			条件Ａ = 1
	ENDIF
	IF !발동済_2(듀얼리스트,4972)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件他所 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 로열(AL)
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件Ｂ = 1
		ENDIF
	ENDIF
	SIF 条件Ａ == 0 && 条件Ｂ == 0
		RETURN 0 
	RETURN 1
ENDIF


@CARDEFFECT_4972(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ
#DIM DYNAMIC 個Ｃ数
#DIM DYNAMIC 選択효과
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 효과後強制誘発
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",300)
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	IF !발동済_1(듀얼리스트,4972)
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		효과条件他所 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1 && GETVAR(@"%듀얼리스트%_MZ_攻:場所") >= 2000
			条件Ａ = 1
	ENDIF
	IF !발동済_2(듀얼리스트,4972)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件他所 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 로열(AL)
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件Ｂ = 1
		ENDIF
	ENDIF
	IF 条件Ａ == 1 && 条件Ｂ == 1
		CALL YN_CHOICE(듀얼리스트,"②이 카드의 攻撃力６００ダウン→필드의 カード１장을 손패に戻す","③自場のこのカード 이외의 モンスター１体덱戻し→その카드의 レベル未満の「로열(AL)」モンスター１体특수소환")
		選択효과 = RESULT
	ELSEIF 条件Ｂ == 1 && 条件Ａ == 0
		選択효과 = いいえ
	ELSEIF 条件Ａ == 1 && 条件Ｂ == 0
		選択효과 = はい
	ENDIF
	IF 選択효과 == はい
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4972,1)
		IF RESULT == 0
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",-600)
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件他所 = 1
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"손패")
		ENDIF
	ELSEIF 選択효과 == いいえ
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4972,2)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件他所 = 1
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"덱")
			選択カードID = RESULT:2
			IF RESULT == 1
				CALL FULLRESET
				효과範囲덱 = 1
				CALLFORM CARD_{選択カードID}("レベル")
				CALL 효과条件レベル_ADV(RESULT - 1,"以下")
				효과条件カテゴリ = 로열(AL)
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ENDIF