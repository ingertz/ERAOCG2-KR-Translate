@CARDNAME_78701,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 마이티 하츠 겟 투 윈!
	CASE "略称"
RESULTS = 마이티 하츠
	CASE "カテゴリ"
RESULTS:0 = HERO
RESULTS:1 = I HERO
RESULTS:2 = 밀리언 라이브!
		; RESULTS:2 = THE IDOLM@STER
RESULTS:3 = 융합
		; RESULTS:4 = MILLION LIVE!
ENDSELECT

@CARD_78701(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 通常마법
ENDSELECT
RETURN 0

@CARDSUMMON_78701(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_78701(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,손패起動,마함起動)
PRINTL ①: 자신의 패/필드에서, "I HERO" 융합 몬스터 카드에 의해 정해진 융합 소재 몬스터를 묘지로 보내고,
PRINTL 그 융합 몬스터 1장을 EX덱에서 융합 소환한다.
CALL CARD_ABLE(2,묘지起動,フリー誘発)
PRINTL ②: 자신 / 상대 배틀 페이즈에 자신 필드의 "I HERO" 융합 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 묘지의 이 카드와 제외 상태인 "I HERO" 몬스터 1장을 덱으로 되돌리고,
PRINTL 턴 종료시까지 대상의 몬스터의 공격력을, 이 효과로 덱으로 되돌린 몬스터의 원래 공격력 / 수비력 중, 높은 쪽의 수치만큼 올린다.

@CARDCAN_78701(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ数

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,78701)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,78701)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL RESETAREA_MAT
	素材範囲손패 = 1
	素材範囲필드 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = I HERO
	효과条件종류 = 융합
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	SIF 발동済_2(듀얼리스트,78701)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件カテゴリ = I HERO
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = I HERO
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_78701(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 보정値

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78701,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲손패 = 1
		CALL FULLRESET
		효과条件カテゴリ = I HERO
		효과条件종류 = 융합
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트,選択カード)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78701,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,덱行き)
		CALL FULLRESET
		효과範囲제외 = 1
		효과条件カテゴリ = I HERO
		효과条件종류 = モン全般
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		選択カードID = RESULT:1
		IF RESULT == 1
			CALLFORM CARD_{選択カードID}("攻撃力")
			보정値 = RESULT
			CALLFORM CARD_{選択カードID}("守備力")
			SIF RESULT > 보정値
				보정値 = RESULT
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件カテゴリ = I HERO
			효과条件종류 = 융합
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一攻",보정値)
			ENDIF
		ENDIF
	ENDIF
ENDIF