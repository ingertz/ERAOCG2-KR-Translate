@CARDNAME_99579,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = E.R.ARG☆S─영광의 아도라
	CASE "略称"
		RESULTS = ERARG☆S─아도라
	CASE "カテゴリ"
		RESULTS:0 = ARG☆S
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_99579(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 지속함정
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 1800
	CASE "分割"
		RETURN 효과モン
	CASE "성별"
		RETURN 牝性
	CASE "連動ID"
		RETURN 1680
ENDSELECT

RETURN 0

@CARD_EXPLANATION_99579(종류)
#DIM DYNAMIC 종류


CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의 ②③ 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 이 카드가 필드에 앞면 표시로 존재하는 한,
PRINTL 자신 필드의 몬스터 존에 존재하는 "ARG☆S" 카드는 함정 카드의 효과를 받지 않는다.
CALLFORM CARD_ABLE(2,마함起動,フリー誘発)
PRINTL ②: 자신 필드에 앞면 표시의 "ARG☆S" 마법 카드가 존재할 경우에 발동할 수 있다.
PRINTL 이 카드는 ③의 효과를 가진 효과 몬스터로서 몬스터 존에 특수 소환한다(함정 카드로도 취급한다).
PRINTL (전사족 / 빛 / 레벨4 / 공1800 / 수1800)
PRINTL 그 후, 이하의 효과에서 1개를 선택하여 적용할 수 있다.
PRINTL ●덱에서 "E.R.ARG☆S─영광의 아도라" 이외의
PRINTL "ARG☆S" 지속 함정 카드 1장을 자신의 마법 & 함정 존에 앞면 표시로 놓는다.
PRINTL ●상대 필드의 앞면 표시의 카드 1장의 효과를 무효로 한다.
CALLFORM CARD_ABLE(3,필드起動,フリー誘発)
PRINTL ③: 이 카드가 몬스터 존에 존재할 경우, 자신 / 상대 턴에 발동할 수 있다.
PRINTL 자신 필드의 앞면 표시의 "ARG☆S" 지속 함정 카드 1장을 제외한다.
PRINTL 그 후, 자신의 묘지 / 제외 상태인 "ARG☆S" 지속 함정 카드 1장을 자신의 마법 & 함정 존에 앞면 표시로 놓는다.
CALL TEXT_DECORATION("ROGUE")

;지속함정となって他のARG☆Sのメンバーと同等の立ち位置になったアドラ。
;首脳陣不在のチーム状況では半ば選手兼任監督のように動く必要があり、負担が多く마함エリアにも立てない状態だったが、
;「あなた」がARG☆S監督に就任することでその問題を（半ば無理矢理に）解決しアドラも後衛役ができるようになった。（という妄想설정）
;後衛としてもクソ優秀で、天性のキャプテンシーを発揮して묘지제외の지속함정と入れ替えるという融通が利きまくる효과。
;자신を제외して자신を戻すという動きをすれば他の지속함정のチームメンバーと同じ挙動になる。名称ターン１にして当然のスペック。

;「ARG☆S」というテーマの挙動を実際に触って検討してみた結果、フィールド＝試合が行われているフィールド、필드が前線、
;마함は後衛でのサポート、묘지や제외はベンチやブルペン・ウォームアップエリア、ロッカールームというイメージに思えた。
;なので、よりアグレッシブに제외に送れるようにし、묘지제외のARG☆Sも柔軟に出し入れできるようにしてテーマ内での戦略性を高める方向性の강화方針。
;監督である「あなた」の一存でARG☆Sのスタメン枠を決められる。特定のメンバーを寵愛したり、逆に干したりもできる感じ。
;「あなた」の欲望の赴くままにチームマネジメントしてARG☆Sを私物化していき、アスリート美少女ハーレムを築いていこう。


@CARDCAN_99579(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99579)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	IF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
			RETURN 0
		RETURN 1
	ENDIF
	SIF ゾーン == "필드"
		RETURN 1
	RETURN 0
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1 || (종류 == フリー誘発 && ゾーン == "마함")
	SIF 발동済_2(듀얼리스트,99579)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件종류 = 마법全般
효과条件カテゴリ = ARG☆S
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,99579,"마함")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || (종류 == フリー誘発 && ゾーン == "필드")
	SIF 발동済_3(듀얼리스트,99579)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	IF 마함数(듀얼리스트) >= 5
		CALL FULLRESET
		효과範囲마함 = 1
효과条件カテゴリ = ARG☆S
		효과条件종류 = 지속함정
		효과条件표시 = 表側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_99579(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 효과使用플래그= 0
#DIM DYNAMIC 選択結果
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 지속효과
	CALL FULLRESET
효과条件カテゴリ = ARG☆S
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"함정耐")
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1 || (종류 == フリー誘発 && ゾーン == "마함")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99579,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"마함",場所)
		IF RESULT == 1
			;추가효과의 발동が可能かの판정
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件표시 = 表側표시
			효과条件除去種 = 無効
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				효과使用플래그 |= 1
			;추가효과의 발동可能かの판정2
			CALL FULLRESET
			효과範囲덱 = 1
효과条件カテゴリ = ARG☆S
			효과条件종류 = 지속함정
효과条件固名前 = E.R.ARG☆S─영광의 아도라
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				효과使用플래그 |= 2
			
			IF 효과使用플래그 & 2 && 효과使用플래그 & 1
				;両方使える場合、どちらを適用するか選択する。CPUは無効を優先する。
				IF 듀얼리스트 == "상대"
					選択結果 = いいえ
				ELSE
					PRINTL 어느 효과를 적용하시겠습니까?
					CALL YN_CHOICE(듀얼리스트,"●덱에서 「E.R.ARG☆S─栄冠のアドラ」 이외의 「ARG☆S」지속 함정 카드 １장을 자신의 마법＆함정 존에 앞면 표시로 둔다。","●상대 필드의 앞면 표시인 카드 １장의 효과를 무효로 한다。 ")
					選択結果 = RESULT
				ENDIF
			ELSEIF 효과使用플래그 & 2
				選択結果 = はい
			ELSEIF 효과使用플래그 & 1
				選択結果 = いいえ
			ELSE
				;どちら의 효과の条件も満たしていない場合、ここでこ의 효과の処理は終了とする
				RETURN 0
			ENDIF
			
			IF 選択結果 == はい
				;덱에서 セットする。
				CALL FULLRESET
				효과範囲덱 = 1
효과条件カテゴリ = ARG☆S
				효과条件종류 = 지속함정
효과条件固名前 = E.R.ARG☆S─영광의 아도라
				CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
			ELSE
				;상대 필드의 カード１장의 효과를 무효로 한다。 
				CALL FULLRESET
				효과条件표시 = 表側표시
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"無効")
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99579)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 필드起動 || (종류 == フリー誘発 && ゾーン == "필드")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99579,3)
	IF RESULT == 0
		;자신의 마함に空きがあるかチェックしつつ필드의 지속함정を제외한다。（空きがない時は필드から제외は할 수 없다。）
		CALL FULLRESET
		효과範囲마함 = 1
		SIF 마함数(듀얼리스트) <= 4
			효과範囲필드 = 1
효과条件カテゴリ = ARG☆S
		효과条件종류 = 지속함정
		효과条件표시 = 表側표시
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			;제외したら、묘지제외から앞면 표시로 置ける。
			CALL FULLRESET
			효과範囲묘지 = 1
			효과範囲제외 = 1
효과条件カテゴリ = ARG☆S
			효과条件종류 = 지속함정
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
		ENDIF
	ENDIF
ENDIF

