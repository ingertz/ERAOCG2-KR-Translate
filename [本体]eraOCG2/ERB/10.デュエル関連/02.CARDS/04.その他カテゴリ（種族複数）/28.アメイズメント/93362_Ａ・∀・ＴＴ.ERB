@CARDNAME_93362,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 어메이즈 어트랙션 스릴 트레인
	CASE "略称"
RESULTS = 에이 ∀ 티 티
	CASE "カテゴリ"
RESULTS:0 = 어트랙션
ENDSELECT

@CARD_93362(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250505
ENDSELECT

RETURN 0

@CARD_EXPLANATION_93362(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,마함起動 ,フリー誘発)
PRINTL ①: 필드의 앞면 표시 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 이 카드를 장착 카드 취급하여 그 몬스터에 장착한다.
CALLFORM CARD_ABLE(2,마함起動 ,フリー誘発)
PRINTL ②: 장착 몬스터의 컨트롤러에 따라 이하의 효과를 발동할 수 있다.
PRINTL ●자신: 장착 몬스터의 표시 형식을 변경하고, 자신의 묘지의 "어트랙션" 함정 카드 1장을 상대가 고른다.
PRINTL 　　　　그 카드를 자신 필드에 세트한다.
PRINTL ●상대: 장착 몬스터를 엔드 페이즈까지 제외한다.

@CARDCAN_93362(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,93362)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	IF 装備先 == 5
		IF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			SIF GETVARS(@"%듀얼리스트%_フィールド名") != "プレシャスパ-ク"
				RETURN 0
		ENDIF
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲상대필드 = 1
		효과条件표시 = 表側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0

	ELSE
		SIF 발동済_2(듀얼리스트,93362)
			RETURN 0
		IF 装備者 == 듀얼리스트
			SIF 마함数(듀얼리스트) >= 5
				RETURN 0
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 어트랙션
			효과条件종류 = 함정全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ELSE
			SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
				RETURN 0
		ENDIF
	ENDIF
	RETURN 1
ENDIF



@CARDEFFECT_93362(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 選択カード名前
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == フリー誘発
	IF 装備先 == 5
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,93362)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲상대필드 = 1
			CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
		ENDIF

	ELSE
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,93362,2)
		IF RESULT == 0
			IF 装備者 == 듀얼리스트
				IF GETVAR(@"%듀얼리스트%_MZ_表:装備先") == 1
					CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"表",2,1)
				ELSEIF GETVAR(@"%듀얼리스트%_MZ_表:装備先") == 2
					CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"表",1,1)
				ENDIF
				CALL FULLRESET
				효과条件カテゴリ = 어트랙션
				효과条件종류 = 함정全般
				CALL CARD_CHOOSE_BASIC(対面者,"상대묘지",ゾーン,場所)
				IF RESULT != 9999
					TRYCALLFORM CARDNAME_{RESULT:99},"名前"
					選択カード名前 = %RESULTS%
					CALL FULLRESET
					효과範囲묘지 = 1
효과条件名前 = %選択カード名前%
					CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
				ENDIF

			ELSE
				CALL FULLRESET
				효과추가종류 = 1
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",装備先,"제외")
			ENDIF
		ENDIF
	ENDIF
ENDIF
