@CARDNAME_58570,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = X•HERO 크로스 가이
	CASE "略称"
RESULTS = 크로스 가이
	CASE "カテゴリ"
RESULTS:0 = HERO
RESULTS:1 = X•HERO
ENDSELECT


@CARD_58570(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1000
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0
@CARDSUMMON_58570(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要링크数 = 2
素材条件종족 = 전사족

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,58570) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_58570(종류)
#DIM DYNAMIC 종류

PRINTL 전사족 몬스터 2장
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없으며,
PRINTL 이 카드의 효과를 발동하는 턴에, 자신은 "HERO" 몬스터밖에 특수 소환할 수 없다.
CALLFORM CARD_ABLE(1,EX소환時)
PRINTL ①: 이 카드가 링크 소환했을 경우, 자신 묘지의 "D-HERO" 몬스터 1장을 대상으로 하고 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 자신 필드의 "D-HERO" 몬스터 1장을 릴리스하고 발동할 수 있다.
PRINTL 릴리스한 몬스터와는 카드명이 다른 "HERO" 몬스터 1장을 덱에서 패에 넣는다.

@CARDCAN_58570(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,58570)
SIF RESULT == 1
	RETURN 0


IF 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,58570)
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = D-HERO
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,58570)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0		
	SIF メインフェイズ != 1
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = D-HERO
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = HERO
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_58570(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 選択カード名前

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58570,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = D-HERO
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		CALL CARD_SS_RESTRICTION(듀얼리스트,"HERO")
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58570,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = D-HERO
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			選択カード_ID = RESULT:1
			CALLFORM CARDNAME_{選択カード_ID}("名前")
			選択カード名前 = %RESULTS%

			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = HERO
			효과条件종류 = モン全般
			효과条件固名前 = %選択カード名前%
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			CALL CARD_SS_RESTRICTION(듀얼리스트,"HERO")
		ENDIF
	ENDIF
ENDIF
