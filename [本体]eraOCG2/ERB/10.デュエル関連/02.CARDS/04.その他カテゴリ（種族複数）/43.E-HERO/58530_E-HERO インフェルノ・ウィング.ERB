@CARDNAME_58530,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = E-HERO 인페르노 윙
	CASE "略称"
RESULTS = 인페르노 W
	CASE "カテゴリ"
RESULTS:0 = HERO
RESULTS:1 = E-HERO
RESULTS:2 = 다크 퓨전
ENDSELECT


@CARD_58530(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2100
	CASE "守備力"
		RETURN 1200
	CASE "특수불가"
		RETURN 1
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_58530(종류)
#DIM DYNAMIC 종류


PRINTL "E•HERO 페더맨" 또는 "E•HERO 버스트 레이디" ＋ "HERO" 몬스터
PRINTL 이 카드는 "다크 퓨전" 카드의 효과로만 특수 소환할 수 있다.
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,특수소환時,EX소환時)
PRINTL ①: 이 카드가 특수 소환했을 경우에 발동할 수 있다.
PRINTL 융합 몬스터를 제외한 "다크 퓨전" 카드 1장을 자신의 덱 / 묘지에서 패에 넣는다.
CALLFORM CARD_ABLE(2,攻撃後強制誘発)
PRINTL ②: 자신의 "HERO" 몬스터가 전투로 상대 몬스터를 파괴했을 경우에 발동한다.
PRINTL 상대에게 2100 데미지를 준다.



@CARDSUMMON_58530(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要素材数 = 2

SIF 효과条件어둠융합 == 0
	RETURN 0
IF EX参照 == 0 || EX参照 == 1
	素材条件名前:0 = E•HERO 페더맨
	素材条件名前:1 = E•HERO 버스트 레이디
ELSEIF EX参照 >= 10
	素材条件カテゴリ = HERO
ENDIF

IF EX参照 == 1
	CALL FUSCAN(듀얼리스트,소환カード,58530)
	RETURN RESULT
ENDIF



@CARDCAN_58530(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,58530)
SIF RESULT == 1
	RETURN 0

IF 종류 == 특수소환時 || 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,58530)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 다크 퓨전
	효과条件종류 = 융합以外
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発
	SIF 발동済_2(듀얼리스트,58530)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)},"カテゴリ"
	SIF !MATCH(RESULTS,"HERO")
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	RETURN 1
ENDIF




@CARDEFFECT_58530(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正

IF 종류 == 특수소환時 || 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58530,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 다크 퓨전
		효과条件종류 = 융합以外
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 攻撃後強制誘発
	CALL LIFE_CHANGE(듀얼리스트,"減少","효과",2100,1)
	CALL CARD_EFFECT_USE(듀얼리스트,58530,2)
ENDIF

