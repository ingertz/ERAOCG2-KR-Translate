@CARDNAME_55503,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 마신의－카리스라임
	CASE "略称"
RESULTS = 카리스라임
	CASE "カテゴリ"
RESULTS:0 = 데빌리추얼
RESULTS:1 = 슬라임
ENDSELECT


@CARD_55503(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 의식
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 1800
	CASE "レア度"
		RETURN SPR
ENDSELECT

RETURN 0
@CARDSUMMON_55503(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要レベル数 = 7

IF EX参照 == 1
	CALL RITCAN(듀얼리스트,소환カード,55503) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_55503(종류)
#DIM DYNAMIC 종류

PRINTL 「마신의의 축탄」에 의해 강림.
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 패의 이 카드를 상대에게 보여주고 발동할 수 있다.
PRINTL 손패를 1장 고르고 버리고, 덱에서 「마신의」 몬스터 1장을 특수 소환한다.
PRINTL 이 턴의 엔드 페이즈에 자신 필드에 의식 몬스터가 존재하지 않을 경우,
PRINTL 자신은 2500 LP를 잃는다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②: 자신 / 상대의 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 묘지의 「마신의」 카드 2장을 덱으로 되돌리고, 덱에서 1장 드로우한다.
PRINTL 그 후, 상대 필드의 카드 1장을 파괴할 수 있다.


@CARDCAN_55503(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,55503)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,55503)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 데빌리추얼
	효과条件종류 = 의식以外
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,55503)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 데빌리추얼
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	IF 듀얼리스트 != "자신" && ターンプレイヤー != 듀얼리스트
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == ターン終了時予約
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 의식
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_55503(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 残りレベル
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,55503,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 데빌리추얼
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT == 1
				CALL CARD_RESERV(듀얼리스트,55503)
		ENDIF
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,55503,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 데빌리추얼
		효과条件複数 = 2
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL CARDDRAW_BASIC(듀얼리스트,1)

			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")
		ENDIF
	ENDIF

ELSEIF 종류 == ターン終了時予約
	CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",2500)
ENDIF