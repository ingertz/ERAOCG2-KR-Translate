@CARDNAME_23161,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 천년 왕조의 방패
	CASE "略称"
RESULTS = 천년의 방패
	CASE "カテゴリ"
RESULTS:0 = 천년
ENDSELECT


@CARD_23161(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 3000
	CASE "성별"
		RETURN 0
	CASE "추가日"
		RETURN 250321
ENDSELECT

RETURN 0

@CARD_EXPLANATION_23161(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 이 카드가 패에 존재할 경우 발동할 수 있다.
PRINTL 이 카드를 자신의 마법 & 함정 존에 앞면 표시로 놓는다.
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ②: 이 카드가 자신의 마법 & 함정 존에 앞면 표시로 존재할 경우,
PRINTL 2000LP를 지불하거나 패의 「천년의 십자」 1장을 상대에게 보여주고 발동할 수 있다.
PRINTL 이 카드를 특수 소환한다.
PRINTL 그 후, 덱에서 「천년의 십자」 1장을 패에 추가할 수 있다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드는 몬스터 존에 존재하는 한, 상대 마법 / 함정 카드의 효과를 받지 않는다.


@CARDCAN_23161(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,23161)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,23161)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF 발동済_2(듀얼리스트,23161)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,23161,"마함")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件名前 = 천년의 십자
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条件달성++
	SIF GETVAR(@"ライフ_%듀얼리스트%") > 2000
		条件달성++
	SIF 条件달성 == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_23161(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,23161,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,23161,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件名前 = 천년의 십자
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件名前 = 천년의 십자
			효과条件任意 = 1
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		ELSE
			RESULT = 9999
		ENDIF
		SIF RESULT == 9999
			CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",2000)
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件名前 = 천년의 십자
			효과条件任意 = 1
			CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"魔耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"함정耐")
ENDIF


