@CARDNAME_73891,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 메멘토 본 파티
	CASE "略称"
RESULTS = 메멘토 B P
	CASE "カテゴリ"
RESULTS:0 = 메멘토
ENDSELECT


@CARD_73891(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73891(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①: 자신 패 / 필드의 "메멘토" 몬스터 1장을 파괴하고,
PRINTL 그 몬스터와는 카드명이 다른 "메멘토" 몬스터 1장을 덱에서 고르고,
PRINTL 패에 넣거나 수비 표시로 특수 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 메인 페이즈에 묘지의 이 카드를 제외하고,
PRINTL 자신 필드의 "메멘토" 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 이 턴, 그 몬스터의 공격은 관통한다.


@CARDCAN_73891(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73891)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 발동済_1(듀얼리스트,73891)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ = 메멘토
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ = 메멘토
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,73891)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 메멘토
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_73891(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 選択カード名前

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73891,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲필드 = 1
		효과条件종류 = モン全般
		효과条件カテゴリ = 메멘토
		효과条件行先플래그 = 破壊行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			選択カード_ID = RESULT:1
			TRYCALLFORM CARDNAME_{選択カード_ID}, "名前"
			選択カード名前 = %RESULTS%
			IF モンスター数(듀얼리스트) < 5
				SIF 듀얼리스트 == "자신"
PRINTL 몬스터를 특수 소환하시겠습니까?
				CALL YN_CHOICE(듀얼리스트)
			ELSE
				RESULT = いいえ
				PRINTW フィールドがいっぱいなので손패に加えます
			ENDIF

			IF RESULT == はい
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = 메멘토
				효과条件종류 = モン全般
				효과条件固名前 = %選択カード名前%
				CALL 효과条件レベル_ADV(8, "以下")
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")

			ELSEIF RESULT == いいえ
				CALL FULLRESET
				효과条件カテゴリ = 메멘토
				효과条件종류 = モン全般
				효과条件固名前 = %選択カード名前%
				CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73891,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件カテゴリ = 메멘토
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"貫通",1,1)
		ENDIF
	ENDIF
ENDIF

