@CARDNAME_71151,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 화염의 검역
	CASE "略称"
RESULTS = 화염의 검역
	CASE "カテゴリ"
RESULTS:0 = 화염의 검사
ENDSELECT


@CARD_71151(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 지속마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71151(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 자신의 "화염의 검사" 몬스터의 소환 / 특수 소환 성공 시에 상대는 카드의 효과를 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ②: 자신의 패/필드에서 몬스터 1장을 묘지로 보내고 발동할 수 있다.
PRINTL 덱 / EX덱에서 "화염의 검사" 1장을 특수 소환한다.
CALLFORM CARD_ABLE(10,攻撃時誘発)
PRINTL ③: 1턴에 1번, 몬스터의 공격 선언 시에,
PRINTL 자신 필드의 전사족 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 턴 종료시까지, 그 몬스터의 공격력을 1000 내리고,
PRINTL 자신 필드의 다른 몬스터의 공격력은 턴 종료시까지 1000 올린다.


@CARDCAN_71151(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71151)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_1(듀얼리스트,71151)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲EX덱 = 1
	효과条件名前 = 화염의 검사
	효과条件소환可 = 1
	효과条件特不無視 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	SIF FINAL_SUMMON_TARGET(듀얼리스트,0) == 5
		RETURN 0
	TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:{FINAL_SUMMON_TARGET(듀얼리스트,0)}")},"カテゴリ"
	SIF !MATCH(RESULTS,"화염의 검사")
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF モンスター数(듀얼리스트) <= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종족 = 전사족
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71151(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM 選択カード


IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71151,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		SIF モンスター数(듀얼리스트) < 5
			효과範囲손패 = 1
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲EX덱 = 1
			효과条件名前 = 화염의 검사
			;효과条件名前:1 = 화염의 검사－투기화염참검
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL SET_PLAYER_INFO(듀얼리스트,"특수阻害中止",3)
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71151)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71151,10)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종족 = 전사족
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一攻",-1000)
			CALL FULLRESET
			효과条件표시 = 表側표시
			CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"一攻",1000)
		ENDIF
	ENDIF
ENDIF