@CARDNAME_23711,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 화염의 검호 MASAKI
	CASE "略称"
RESULTS = 화염의 마사키
	CASE "カテゴリ"
RESULTS:0 = 화염의 검사
RESULTS:1 = 게임 오리지널
ENDSELECT

@CARD_23711(参照先)
;名前専用
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1100
	CASE "守備力"
		RETURN 1100
	CASE "성별"
		RETURN 雄性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_23711(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드 명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動 ,フリー誘発)
PRINTL ①: 자신 / 상대 턴에 발동할 수 있다.
PRINTL 덱에서 「화염의 검사」, 「사라만드라」 카드 1장을 묘지로 보낸다.
PRINTL 그 후, 자신의 필드 마법 & 함정 존의 몬스터를 융합 소재로 하여,
PRINTL 화염 속성의 융합 몬스터 1장을 융합 소환할 수 있다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 묘지의 이 카드를 제외하고,
PRINTL 자신 필드의 「화염의 검사」 융합 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 융합 몬스터에게 완전 내성을 부여한다.

@CARDCAN_23711(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,23711)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,23711)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ:0 = 화염의 검사
	효과条件カテゴリ:1 = 샐러맨더
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,23711)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 화염의 검사
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_23711(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,23711,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ:0 = 화염의 검사
		효과条件カテゴリ:1 = 샐러맨더
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
		IF RESULT == 1
			CALL RESETAREA_MAT
			素材範囲필드 = 1
			素材範囲마함 = 1
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件속성 = 화염속성
			효과条件소환可 = 1
			효과条件任意 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL FUSSUMMON(듀얼리스트,選択カード)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,23711,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件カテゴリ = 화염의 검사
		효과条件종류 = 융합
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"魔耐",2,1)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"함정耐",2,1)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"モ耐",2,1)
		ENDIF
	ENDIF
ENDIF