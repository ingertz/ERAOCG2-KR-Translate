@CARDNAME_35662,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 룡아 류진 도스팡
	CASE "略称"
RESULTS = 도스팡
	CASE "カテゴリ"
RESULTS:0 = 사무라이
RESULTS:1 = 크로스 기어
RESULTS:4 = 듀얼 마스터즈
ENDSELECT


@CARD_35662(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 지속마법
	CASE "分割"
		RETURN 크로스기어
ENDSELECT

RETURN 0

@CARD_EXPLANATION_35662(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드 이름의 ①③의 효과는 1턴에 1번, 어느 하나만 사용할 수 있다.
PRINTL 이 카드는 패에서 「사무라이」 카드 1장을 버리지 않으면 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 이 카드의 발동 시의 효과 처리로서,
PRINTL 자신 덱 위에서 3장을 넘기고, 그 중에서 「사무라이」 카드 1장을 고른다.
PRINTL 이렇게 몬스터를 고른 경우, 그 카드를 몬스터 존에 특수 소환하고,
PRINTL 지속 마법 카드를 고른 경우, 그 카드를 앞면 표시로 세트하고 그 패에서 발동했을 때의 능력을 발동할 수 있다.
CALLFORM CARD_ABLE(10,마함起動)
PRINTL ②: 1턴에 1번, 자신 메인 페이즈에 발동할 수 있다.
PRINTL 이 카드를 자신의 「사무라이」 몬스터 1체에 장착한다.
CALLFORM CARD_ABLE(1,攻撃時誘発)
PRINTL ③: 이 카드를 장착한 몬스터의 공격 선언 시에 발동할 수 있다.
PRINTL 이 카드의 발동 시의 효과를 적용한다.


@CARDCAN_35662(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,35662)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件표시 = 表側표시
	효과条件カテゴリ = 사무라이
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF 발동済_1(듀얼리스트,35662)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 사무라이
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;とりあえずチェックだけ
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)

	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF 발동済_1(듀얼리스트,35662)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF 装備先 == 5
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_戦闘カード") != 装備先
		RETURN 0
	SIF 現山札数(듀얼리스트) <= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_35662(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者

#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35662,10)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 사무라이
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35662,1)
	IF RESULT == 0
		IF (종류 == 마함起動 || 종류 == 손패起動) && ゾーン != "효과処理"
			CALL FULLRESET
			효과条件カテゴリ = 사무라이
			효과条件他所 = 1
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ENDIF

		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		CALL FULLRESET
		효과条件カテゴリ = 사무라이
		효과条件종류:0 = 효과モン
		효과条件종류:1 = 通常モン
		효과条件종류:2 = 지속마법
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "덱",ゾーン,場所,3)	
		IF RESULT != 9999
			選択カード = RESULT
			選択カード_ID = RESULT:99
			CALLFORM CARD_{選択カード_ID}("종류")
			IF RESULT == 지속마법
				CALL CARD_DISPOSE(듀얼리스트,"덱",選択カード,마함行き,1)
				CALLFORM CARDEFFECT_{選択カード_ID}(듀얼리스트,손패起動,"효과処理")
			ELSE
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트, "덱", 選択カード)
			ENDIF
		ENDIF
	ENDIF
ENDIF