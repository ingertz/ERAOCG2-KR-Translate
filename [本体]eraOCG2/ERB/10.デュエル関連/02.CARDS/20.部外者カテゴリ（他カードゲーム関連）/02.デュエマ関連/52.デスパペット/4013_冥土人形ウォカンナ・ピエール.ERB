@CARDNAME_4013,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 명부인형 워칸나 피에르
	CASE "略称"
RESULTS = 명부 워칸나
	CASE "カテゴリ"
RESULTS:0 = 데스 퍼펫
RESULTS:4 = 듀얼 마스터즈
ENDSELECT


@CARD_4013(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1300
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4013(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 한 번만 사용할 수 있다.
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ①: 자신이 상대의 공격으로 데미지를 받았을 경우 발동할 수 있다.
PRINTL 패 / 묘지에서 이 카드를 특수 소환한다.
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 소환 / 특수 소환되었을 경우 발동할 수 있다.
PRINTL 상대의 덱 맨 위 카드를 묘지로 보낸다.
PRINTL 묘지로 보낸 카드가 몬스터 카드였을 경우,
PRINTL 그 몬스터의 공격력 이하의 공격력을 가진, 상대 필드의 몬스터 1장을 패로 되돌린다.


@CARDCAN_4013(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4013)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,4013)
		RETURN 0
	SIF !GROUPMATCH(ゾーン,"손패","묘지")
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL CARD_CHECK_FIN_BIT(듀얼리스트,"戦闘ダメージ")
	SIF RESULT != 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4013,ゾーン)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,4013)
		RETURN 0
	SIF 現山札数(対面者) < 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4013(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 選択カード攻撃力
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4013,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4013,2)
	IF RESULT == 0
		TRYCALLFORM CARD_{GETVAR(@"%対面者%_덱:0")}, "종류"
		SIF RESULT < モン全般
			条件달성 = 1
		TRYCALLFORM CARD_{GETVAR(@"%対面者%_덱:0")}, "攻撃力"
		選択カード攻撃力 = RESULT
		SIF 選択カード攻撃力 <= 0
			条件달성 = 0
		CALL CARD_DECK_DISPOSE(対面者,"덱",1)
		IF 条件달성 == 1
			CALL FULLRESET
			효과条件攻撃力 = 選択カード攻撃力
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"손패")
		ENDIF
	ENDIF
ENDIF

