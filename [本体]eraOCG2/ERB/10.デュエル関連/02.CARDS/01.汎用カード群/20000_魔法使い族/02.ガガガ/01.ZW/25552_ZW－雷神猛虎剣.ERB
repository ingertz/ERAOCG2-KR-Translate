@CARDNAME_25552,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 제알 웨폰－라이트닝 블레이드
	CASE "略称"
RESULTS = ZW－뇌신맹호검
	CASE "カテゴリ"
RESULTS:0 = 제알 웨폰
RESULTS:1 = 제알
ENDSELECT


@CARD_25552(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 2000
	CASE "分割"
		RETURN 유니온
ENDSELECT

RETURN 0

@CARD_EXPLANATION_25552(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,손패起動,필드起動)
PRINTL ①：자신 필드의 「유토피아」 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 자신 패 / 필드에서 이 카드를 공격력 1200 올리는 장착 카드로서 대상 몬스터에게 장착한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②：이 카드가 장착되어 있는 경우, 장착 몬스터와 자신 필드의 「ZW」 카드는 파괴 내성을 가진다.

@CARDCAN_25552(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,25552)
SIF RESULT == 1
	RETURN 0

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 손패起動 || 종류 == 필드起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 유토피아
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動 || 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF 装備先 == 5
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_25552(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動 || 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25552)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 유토피아
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_EQUIP_MONSTER(듀얼리스트,選択カード,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"装備",場所,1200)
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"破耐")

	CALL FULLRESET
	효과条件カテゴリ = 제알 웨폰
	CALL STATCHANGE_EFFECT(듀얼리스트,"마함",場所,"破耐")
ENDIF

