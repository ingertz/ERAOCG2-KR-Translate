@CARDNAME_71173,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 절대빙벽－커튼 오브 헤일
	CASE "略称"
RESULTS = 절대빙벽
	CASE "カテゴリ"
RESULTS:0 = 블리자드
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_71173(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
	CASE "추가日"
		RETURN 250615
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71173(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 자신 필드는 "절대빙벽"이 된다.
PRINTL ②: 이 카드가 필드에 존재하는 한, 자신은 EX덱에서 물 속성 몬스터밖에 특수 소환할 수 없으며,
PRINTL 자신 필드의 마법사족 / 물 속성 몬스터는 전투 내성을 얻는다.
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ③: 자신 메인 페이즈에 패를 1장 버리고 발동할 수 있다.
PRINTL 자신의 덱/묘지에서 레벨 2 이하의 마법사족 / 물 속성 몬스터 1장을 수비 표시로 특수 소환한다.

;テーマとしてのまとまりを作るには、やはりフィールド마법ないし지속마법を貼るという動きが必要という考え方で추가。
;近年よく見る、テーマとして動くことを強いる制約が常時かかる타입のデメリット효과ちょい足し、他は強いこと書いてある。という体裁のカード。
;もともと카드로서は「絶対氷壁」と「カーテン・オブ・ヘイル」で別々に存在していたが、효과를 見直して１장にまとめてしまった。
;全体的に【블리자드 프린세스】は展開するとしっかり패를 消費していく타입のデザインになるよう意図的に調整していて、それは少し古いデザイン。
;完全な現代水準だと패를 減らすことなく盤面を作れるデザインが多いが、상대ターン「블리자드 프린세스」のロック性能を評価して加減している。


@CARDCAN_71173(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 条件達成カード名,10
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71173)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_2(듀얼리스트,71173)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 손패数(듀얼리스트) == 0
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件종류 = モン全般
	효과条件종족 = 마법사족
	효과条件속성 = 물속성
	CALLFORM 효과条件レベル_ADV(2, "以下")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71173(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 選択カード名前
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 条件達成カード名,10
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 条件達成数 = 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71173,2)
	IF RESULT == 0
		;손패１장을 묘지へ送って특수소환
		CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과条件종류 = モン全般
			효과条件종족 = 마법사족
			효과条件속성 = 물속성
			CALLFORM 효과条件レベル_ADV(2, "以下")
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71173)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 지속효과
	;絶・対・零・度！　ではない
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "絶対氷壁")
	;EX물속성縛り
	CALL SET_TMP_EX_RST(듀얼리스트,"속성",물속성)
	;戦闘耐性付与
	CALL FULLRESET
	효과条件종류 = モン全般
	효과条件종족 = 마법사족
	효과条件속성 = 물속성
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"戦耐",3)
ENDIF

