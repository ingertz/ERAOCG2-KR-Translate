@CARDNAME_71172,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 혹한의 고드름
	CASE "略称"
RESULTS = 혹한의 고드름
	CASE "カテゴリ"
RESULTS:0 = 블리자드
ENDSELECT


@CARD_71172(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
	CASE "추가日"
		RETURN 250615
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71172(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 한 번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①: 자신 / 상대 메인 페이즈에, 이하의 효과 중 1개를 선택하여 발동할 수 있다.
PRINTL (이 효과 발동 후, 턴 종료시까지 자신은 EX덱에서 마법사족/물 속성 몬스터밖에 특수 소환할 수 없다.)
PRINTL ● 자신 필드에 '고드름 토큰'(마법사족/물/레벨 4/공격력 1900/수비력 1200) 2장을 수비 표시로 특수 소환한다.
PRINTL ● 자신 필드에 레벨 8 이상의 마법사족/물 속성 몬스터가 존재할 경우, 상대 필드(앞면 표시)의 카드 1장의 효과를 턴 종료시까지 무효로 한다.
PRINTL 그 후, 상대 필드에 '고드름 토큰'(마법사족/물/레벨 4/공격력 1900/수비력 1200)을 2장까지 수비 표시로 특수 소환할 수 있다.
PRINTL (이 효과로 생성된 '고드름 토큰'은, 턴 종료시에 소멸한다.)
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다. 패의 물 속성 몬스터 1장을 소환한다.

;プレミアパック2025で「아이스 돌」等と共に추가されたカード。자신 필드에 고드름 토큰２体を生成する효과と、
;묘지제외で물속성モンスター１体の소환を行う효과를 持っている。元々のカードではトークン生成後の制約がかなり強くて扱いづらく、
;アドバンス소환のリリース素材にするためか、単なる壁としての運用しか想定されていないので、制約を若干緩める。
;とはいえ、この制約下で出せるのは「블리자드 프린세스」以外だとエリア、융합ヴァリアンツ、ヴァーラル、ドラゴンテイル、イヴ関連ぐらい
;副葬で送って물속성の소환権として使うほうが使いやすそうではある。副葬は他の묘지효과持ちの마함や氷結界を送る事も可能で使い出がある。
;
;【블리자드 프린세스】での専用효과として、フィールドにレベル８以上の水마법使いが存在していれば상대 필드의 表側표시カード１장의 無効化という
;使い勝手のよい無効효과に加えて、상대 필드에 고드름 토큰を生やせる효과もついていて、상대 필드を氷の世界に変えてしまう。
;융합体とのコンボで全部ぶっ壊してキルを取る使い方を想定しているが、상대ターンの妨害手段として使った場合のトークン生成は注意が必要。
;상대は自由にトークンをEX몬스터의 素材に使えてしまうので、상대ターンに軽率にトークンを出すと逆に利用されることになりかねない。

@CARDCAN_71172(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件達成 = 0

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71172)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,71172)
		RETURN 0
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	;자신의 필드에 고드름 토큰を２つ出せるか確認
	CALL CARD_SS_CHECK(듀얼리스트,120082,"")
	IF RESULT != 1
		SIF モンスター数(듀얼리스트) <= 3
			条件達成 = 1
	ENDIF
	;出せない場合も、자신 필드에 レベル８以上の水마법使いが存在する場合かつ
	;상대 필드에 表側표시カード１장以上ある場合なら、２つめ의 효과의 발동条件を満たしており발동可能。
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종족 = 마법사족
	효과条件속성 = 물속성
	효과条件종류 = モン全般
	CALLFORM 효과条件レベル_ADV(8, "以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		효과条件표시 = 表側표시
		효과条件除去種 = 無効
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件達成 = 1
	ENDIF
	SIF !条件達成
		RETURN 0
	
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,71172)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件속성 = 물속성
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		RETURN 0
	ELSE
		;レベル４以下がいない場合의 발동판정（上級しか居ない場合、リリースするモンスターが자신 필드에 必要）
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件속성 = 물속성
		효과条件종류 = モン全般
		CALLFORM 효과条件レベル_ADV(4, "以下")
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT != 1
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ENDIF
	ENDIF
	
	RETURN 1
ENDIF

@CARDEFFECT_71172(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 選択カード名前
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 条件達成カード名,10
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 条件達成 = 0
#DIM DYNAMIC 選択結果

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71172,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;まず、자신의 필드에 고드름 토큰を出せるか確認、フィールドに空き２つ必要
		CALL CARD_SS_CHECK(듀얼리스트,120082,"")
		IF RESULT != 1
			SIF モンスター数(듀얼리스트) <= 3
				条件達成 |= 1
		ENDIF
		;자신 필드의 「블리자드 프린세스」と상대 필드의 카드를 確認
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종족 = 마법사족
		효과条件속성 = 물속성
		효과条件종류 = モン全般
		CALLFORM 효과条件レベル_ADV(8, "以上")
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件표시 = 表側표시
			효과条件除去種 = 無効
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件達成 |= 2
		ENDIF
		
		IF 条件達成 & 1 && 条件達成 & 2
			;両方의 효과의 발동条件を満たしていれば、選択肢を표시。CPUは자신 필드에 氷柱を出す（＝はい）
			IF 듀얼리스트 == "상대"
				選択結果 = はい
			ELSE
				;효과の用途を聞く
PRINTL 어느 효과를 사용하시겠습니까?
				CALL YN_CHOICE(듀얼리스트,"●자신 필드에 고드름 토큰（마법사족・水・星４・攻１９００／守１２００）２体を守備표시で특수소환する。","●상대 필드（表側표시）のカード１장의 효과를 ターン終了時まで무효로 한다。 ")
				選択結果 = RESULT
			ENDIF
		ELSEIF 条件達成 & 1
			選択結果 = はい
		ELSEIF 条件達成 & 2
			選択結果 = いいえ
		ELSE
			;どちら의 효과の条件も満たしてない状態で迷い込んだら、효과処理しないようにしている。
			選択結果 = -1
		ENDIF
		
		IF 選択結果 == はい
			FOR カウンタ,0,2
				CALL CARD_SS_CHECK(듀얼리스트,120082,"")
				SIF RESULT == 1
					BREAK
				CALL FULLRESET
				CALL CARD_GENERATE(듀얼리스트,120082,"守備표시")
			NEXT
		ELSEIF 選択結果 == いいえ
			;いいえ의 효과：상대 필드의 カード１장을 選んで無効。その後상대 필드에 ２体までトークンを生成
			CALL FULLRESET
			효과条件표시 = 表側표시
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"無効")
			IF RESULT == 1
				FOR カウンタ,0,2
					SIF モンスター数(対面者) >= 5
						BREAK
					IF 듀얼리스트 == "자신"
PRINTL 상대 필드에 고드름 토큰을 수비 표시로 특수 소환하시겠습니까?
						CALL YN_CHOICE(듀얼리스트)
						選択結果 = RESULT
					ENDIF
					
					IF 듀얼리스트 == "상대" || 選択結果 == はい
						CALL FULLRESET
						CALL CARD_GENERATE(듀얼리스트,120082,"守備표시",1)
					ELSE
						BREAK
					ENDIF
				NEXT
			ENDIF
		ENDIF
		;EX:EX덱에서 の소환禁止の制約（마법사족물속성のみ可能）
		CALL FULLRESET
		EX소환可能종족 = 마법사족
		EX소환可能속성 = 물속성
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71172,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		;패에서 물속성モンスター１体を소환する処理
		;参考：冥帝エレボス
		CALL FULLRESET
		효과条件속성 = 물속성
		효과条件종류 = モン全般
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			選択カード_ID = RESULT:99
			TRYCALLFORM CARD_{選択カード_ID},"レベル"
			IF RESULT >= 5
				;レベル５以上の소환の場合、자신 필드의 モンスター１体を묘지へ送る。
				CALL FULLRESET
				효과範囲필드 = 1
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
				SIF	RESULT != 1
					RETURN 0
			ENDIF
			CALL CARD_SUMMON_SHORI(듀얼리스트,選択カード,"攻撃표시",1)
		ENDIF
	ENDIF
ENDIF
