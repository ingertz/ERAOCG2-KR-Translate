@CARDNAME_71171,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 영혼을 불어넣는 조각가
	CASE "略称"
RESULTS = 영혼불넣 조각가
	CASE "カテゴリ"
RESULTS:0 = 블리자드
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_71171(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "レア度"
		RETURN SPR
	CASE "추가日"
		RETURN 250615
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71171(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 자신의 덱 / 묘지에서 「아이스 돌」 1장을 패에 넣는다.
PRINTL 자신 필드에 레벨 5 이상의 마법사족・물 속성 몬스터가 존재할 경우, 패에 넣지 않고 수비 표시로 특수 소환할 수도 있다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 필드에 「블리자드 프린세스」 몬스터 및 「고드름 토큰」 또는 「아이스 돌」이 존재할 경우,
PRINTL 자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신 필드의 모든 「고드름 토큰」 및 「아이스 돌」을 「블리자드 프린세스」로 변신시킨다.
PRINTL (이 효과로 변신한 「블리자드 프린세스」는, 턴 종료 시에 소멸한다.)
PRINTL 그 후, 자신 필드의 「블리자드 프린세스」 몬스터를 모두 공격 표시로 한다.

;氷の精霊たちの中でも氷像作りの"匠"として尊崇されている芸術家。
;自律式氷人形「아이스 돌」は彼の革命的な作品だったが、大量生産のためにデザイン性が疎かになった点は悔いを残している。
;「블리자드 프린세스」が臣民に披露する氷像は、いつも彼に泣きついて代わりに作ってもらっている。
;プリンセスを敬愛しているために、どうしても頼まれると断れないとは本人の弁。生き写しのような姫の氷像を作る匠の業も忠義からの行い。

@CARDCAN_71171(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 条件達成カード名,10
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71171)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,71171)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
효과条件名前 = 아이스 돌
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,71171)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件名前:0 = 블리자드 프린세스
	효과条件名前:1 = 앱솔루트 블리자드 프린세스
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件名前:0 = 고드름 토큰
	효과条件名前:1 = 아이스 돌
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71171(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件達成
#DIM DYNAMIC 選択結果
#DIM DYNAMIC 選択カード_ID

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71171,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;まずフィールドにレベル５以上の水마법使いが存在するか確認
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종류 = モン全般
		효과条件속성 = 물속성
		효과条件종족 = 마법사족
		CALLFORM 효과条件レベル_ADV(5, "以上")
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			IF 듀얼리스트 == "상대"
				選択結果 = はい
			ELSE
PRINTL 「아이스 돌」을 필드에 수비 표시로 특수 소환하시겠습니까?
				CALL YN_CHOICE(듀얼리스트,"フィールドに守備표시で특수소환する。","손패に加える。")
				選択結果 = RESULT
			ENDIF
		ELSE
			選択結果 = いいえ
		ENDIF
		
		IF 選択結果 == はい
			;フィールドに守備표시で특수소환する。
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
효과条件名前 = 아이스 돌
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
		ELSE
			;손패に加える
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
효과条件名前 = 아이스 돌
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71171,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;자신 필드의 「아이스 돌」と「고드름 토큰」をすべて「블리자드 프린세스」に변경する。
		;実際は同一名称のトークンモンスターに変化する処理と攻撃표시にする処理を行う。
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
				CONTINUE
			SIF GETVAR(@"%듀얼리스트%_MZ_表:カウンタ") == 3
				CONTINUE
			選択カード_ID = GETVAR(@"%듀얼리스트%_필드:カウンタ")
			TRYCALLFORM CARDNAME_{選択カード_ID},"名前"
			SIF !GROUPMATCH(RESULTS,"아이스 돌","고드름 토큰")
				CONTINUE
			;「아이스 돌」か고드름 토큰であることを確認したので카드를 ID:171171に변경
			;参考：メタル・インベード・アーマー強襲
			CALL CARD_RECONSTRUCT(듀얼리스트,"필드",カウンタ,171171)
			CALL CHANGE_BANISH_ID(듀얼리스트,カウンタ,選択カード_ID)
		NEXT
		
		;みんな、氷柱は持ったな！！　いくぞォ！！
		CALL FULLRESET
		효과条件名前:0 = 블리자드 프린세스
		효과条件名前:1 = 앱솔루트 블리자드 프린세스
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"表",1)
	ENDIF
ENDIF


;---------------------------------------------------------------------------------------
;블리자드 프린세스（고드름 토큰などの変身）のカードデータ部分
;---------------------------------------------------------------------------------------

@CARDNAME_171171,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 블리자드 프린세스
	CASE "略称"
RESULTS = 블리자드 P
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_171171(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 2100
ENDSELECT


@CARD_EXPLANATION_171171(종류)
#DIM DYNAMIC 종류
PRINTL 「블리자드 프린세스」의 판박이 같은 훌륭한 얼음 조각상.
PRINTL 영혼을 불어넣는 조각가에 의한 장인 정신으로 새겨진 얼음 조각상은, 본인과 동등한 전투 능력도 지닌다.
PRINTL 본래는 대역으로서 공주 대신 싸울 예정인데, 프린세스는 항상 얼음 조각상을 이끌고 선두에 서서 적에게 돌진해 간다.
PRINTL 
PRINTL 판박이 모습이 되어도 성격까지는 모방되지 않는 듯하여, 본체보다 얌전하고 정숙한 기질을 지닌다.
PRINTL 본인보다 공주답다고 일컬어지는 경우가 많아, 프린세스는 불만스러워하는 것 같다.

@CARDCAN_171171(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,20082)
SIF RESULT == 1
	RETURN 0

IF 종류 == ターン終了時強制
	RETURN 1
ENDIF

@CARDEFFECT_171171(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

IF 종류 == ターン終了時強制
	;「블리자드 프린세스」の氷像は溶けて消えてしまいました・・・あーあ
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,消滅行き)
ENDIF



