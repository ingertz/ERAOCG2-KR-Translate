@CARDNAME_73301,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 텔레포트 융합
	CASE "略称"
RESULTS = 텔레융합
	CASE "カテゴリ"
RESULTS:0 = 텔레포트
RESULTS:1 = 융합
ENDSELECT


@CARD_73301(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
	CASE "추가日"
		RETURN 250430
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73301(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①: 자신 / 상대의 메인 페이즈에 발동할 수 있다.
PRINTL 자신 필드 / 묘지의 사이킥족 몬스터를 융합 소재로서 제외하고,
PRINTL 사이킥족의 융합 몬스터 1장을 융합 소환한다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ②: EX덱에서 특수 소환된 자신 필드의 사이킥족 몬스터가 전투 / 효과로 파괴되었을 경우,
PRINTL 묘지의 이 카드를 제외하고, 자신의 제외된 사이킥족 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.


@CARDCAN_73301(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73301)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 발동済_1(듀얼리스트,73301)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲묘지 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件종족 = 사이킥족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後誘発
	SIF 발동済_2(듀얼리스트,73301)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과条件종족 = 사이킥족
	효과条件召種 = 召種EX덱
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件종족 = 사이킥족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_73301(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73301,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲묘지 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件종족 = 사이킥족
		효과条件소환可 = 1	
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード,제외行き)
		ENDIF
	ENDIF

ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73301,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲제외 = 1
		효과条件종족 = 사이킥족
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF