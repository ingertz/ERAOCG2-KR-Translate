@CARDNAME_72111,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 초룡 재앙
	CASE "略称"
RESULTS = 초룡 재앙
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_72111(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
	CASE "レア度"
		RETURN SPR
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72111(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 1턴에 1번, 어느 한 효과밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動,마함起動,フリー誘発)
PRINTL ①: 자신의 패 / 묘지 / 제외 상태의 "정룡" 몬스터 1장을 특수 소환한다.
PRINTL 그 후, 자신 필드의 "정룡" 몬스터만을 소재로 하여 "정룡" 엑시즈 몬스터 1장을 엑시즈 소환할 수 있다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신의 제외 상태의 "정룡" 몬스터를 원하는 만큼 묘지로 되돌린다. (같은 이름은 1장까지)


@CARDCAN_72111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72111)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 발동済_1(듀얼리스트,72111)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,,)
	SIF RESULT == 1
		RETURN 0

	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件カテゴリ = 정룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,72111)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件カテゴリ = 정룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_72111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 攻撃力보존
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72111,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 정룡
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲EX덱 = 1
			효과条件종류 = 엑시즈
			효과条件カテゴリ = 정룡
			효과条件소환可 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				CALL RESETAREA_MAT
				CALL FULLRESET
				효과条件종류 = 엑시즈
				효과条件カテゴリ = 정룡
				효과条件소환可 = 1
				효과条件任意 = 1
				CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
				選択カード = RESULT
				IF 選択カード != 9999
					TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
					CALL XYZSUMMON(듀얼리스트, 選択カード)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72111,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲제외 = 1
		효과条件カテゴリ = 정룡
		효과条件行先플래그 = 덱行き
		효과条件複数 = 任意の数
		효과条件固名前選択 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

