@CARDNAME_21103,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 딥 오브 푸른 눈
	CASE "略称"
RESULTS = 심연의 푸른 눈
	CASE "カテゴリ"
RESULTS:0 = 푸른 눈의
ENDSELECT


@CARD_21103(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2500
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21103(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없으며,
PRINTL 자신 필드 또는 자신의 묘지에 「푸른 눈의 백룡」이 존재할 경우에만 발동할 수 있다.
CALLFORM CARD_ABLE(1,특수소환時)
PRINTL ①: 이 카드가 특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 덱에서 「카오스 폼」 또는 「융합」 1장을 패에 더한다.
CALLFORM CARD_ABLE(2,ターン終了時)
PRINTL ②: 자신 엔드 페이즈에 발동할 수 있다.
PRINTL 덱에서 레벨 8 이상의 드래곤족 몬스터 1장을 패에 더한다.
CALLFORM CARD_ABLE(3,묘지起動)
PRINTL ③: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신 필드의 모든 레벨 8 이상의 드래곤족 몬스터의 공격력은 1000 올린다.


@CARDCAN_21103(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21103)
SIF RESULT == 1
	RETURN 0

IF 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,21103)
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 푸른 눈의 백룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件名前:0 = 융합
	효과条件名前:1 = 카오스 폼
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時
	SIF 발동済_2(듀얼리스트,21103)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 푸른 눈의 백룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_3(듀얼리스트,21103)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 푸른 눈의 백룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 드래곤족
	CALL 효과条件レベル_ADV(8, "以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21103(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21103,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件名前:0 = 융합
		효과条件名前:1 = 카오스 폼
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21103,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종족 = 드래곤족
		CALL 효과条件レベル_ADV(8, "以上")
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21103,3)
	IF RESULT == 0
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_필드:{カウンタ}") == -1
				CONTINUE
			TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:{カウンタ}")}, "レベル"	
			SIF RESULT < 8
				CONTINUE
			TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:{カウンタ}")}, "종족"	
			SIF RESULT != 드래곤족
				CONTINUE
			CALL SET_FIELD_INFO(듀얼리스트,"필드",カウンタ,"永攻",1000)
		NEXT
	ENDIF
ENDIF



