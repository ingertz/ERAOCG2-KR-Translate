@CARDNAME_21102,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 푸른 눈의 제트 드래곤
	CASE "略称"
RESULTS = 푸른 눈 제트
	CASE "カテゴリ"
RESULTS:0 = 푸른 눈의
ENDSELECT


@CARD_21102(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 0
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21102(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없으며,
PRINTL 자신 필드 또는 자신의 묘지에 「푸른 눈의 백룡」이 존재할 경우에만 발동할 수 있다.
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ①: 이 카드가 패/묘지에 존재하고, 필드의 카드가 전투 / 효과로 파괴되었을 경우에 발동할 수 있다.
PRINTL 이 카드를 특수 소환한다.
CALLFORM CARD_ABLE(2,攻撃時誘発)
PRINTL ②: 이 카드가 전투를 실행할 경우, 상대 필드의 카드 1장을 대상으로 발동할 수 있다.
PRINTL 그 카드를 주인의 패로 되돌린다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드 이외의 자신 필드의 카드는 파괴 내성을 얻는다.


@CARDCAN_21102(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21102)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,21102)
		RETURN 0
	SIF !GROUPMATCH(ゾーン,"손패","묘지")
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,21102,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 푸른 눈의 백룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般",2)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,21102)
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 푸른 눈의 백룡
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21102(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21102,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン, 場所)
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21102,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"손패")
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件他所 = 1
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"破耐")
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"마함",場所,"破耐")
ENDIF



