@CARDNAME_50598,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 궁극환신 알티미틀 비시바-르킨
	CASE "略称"
RESULTS = 비시바-르킨
	CASE "カテゴリ"
RESULTS:0 = 궁극신
ENDSELECT


@CARD_50598(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "특수불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARDSUMMON_50598(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_50598(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 싱크로 소환할 수 없으며, 자신 묘지에 「궁극신 알티마야 촐킨」이 존재하고,
PRINTL 자신 필드의 앞면 표시의 레벨 8 이상으로 같은 레벨의,
PRINTL 튜너와 튜너 이외의 몬스터를 1장씩 묘지로 보냈을 경우에만 특수 소환할 수 있다.
CALL CARD_ABLE(0,지속효과)
PRINTL ①: 이 카드는 전투 / 효과로는 파괴되지 않으며, 공격력은 필드의 몬스터의 수× 1000 올린다.
CALL CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ②: 자신 / 상대 턴에 1번, 발동할 수 있다. 서로의 필드에 「사안신 토큰」을 가능한 한 수비 표시로 생성한다.
CALL CARD_ABLE(0,攻撃後誘発)
PRINTL ③: 이 카드가 전투를 실행했을 경우에 발동할 수 있다. 상대 필드의 몬스터를 전부 파괴하고, 그 수× 200 데미지를 상대에게 준다.
PRINTL 
PRINTL 「사안신(우차치미-메) 토큰」
PRINTL 악마족 / 어둠 / 레벨 1 / 공격력 0 / 수비력 0
PRINTL 이 토큰은 소재로 할 수 없다.

@CARDCAN_50598(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 条件レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50598)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,50598)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
효과条件名前 = 궁극신 알티마야 촐킨
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;参考先：ラティス
	FOR カウンタ,0,5
		SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
			CONTINUE
		TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:カウンタ")},"종류"
		SIF GROUPMATCH(RESULT,엑시즈,링크)
			CONTINUE
		SIF GETVAR(@"%듀얼리스트%_MZ_Ｔ:カウンタ") >= 1
			CONTINUE
		SIF GETVAR(@"%듀얼리스트%_MZ_星:カウンタ") < 8
			CONTINUE
		条件レベル = GETVAR(@"%듀얼리스트%_MZ_星:カウンタ")
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件튜너 = 1
		효과条件レベル = 条件レベル
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件달성++
	NEXT
	SIF 条件달성 == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5 && モンスター数(対面者) >= 5
		RETURN 0
	RETURN 1	
ELSEIF 종류 == 지속효과 || 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_50598(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC モンスター総数
#DIM DYNAMIC 選択レベル
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50598)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件튜너 = -1
		CALLFORM 효과条件レベル_ADV(8, "以上")
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択レベル = GETVAR(@"%듀얼리스트%_MZ_星:選択カード")
			CALL FULLRESET
			효과条件튜너 = 1
			효과条件レベル = 選択レベル
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과条件튜너 = -1
				효과条件レベル = 選択レベル
				CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
				IF RESULT == 1
					CALL FULLRESET
					효과条件特不無視 = 1
					CALL CARD_EX_SUMMON(듀얼리스트, "EX덱", 場所)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50598,10)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_GENERATE(듀얼리스트,150598,"守備표시",0,5)
		CALL FULLRESET
		CALL CARD_GENERATE(듀얼리스트,150598,"守備표시",1,5)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"破耐")
ELSEIF 종류 == 攻守変動
	モンスター総数 = モンスター数(듀얼리스트) + モンスター数(対面者)
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,モンスター総数 * 1000)
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50598)
	IF RESULT == 0
		CALL FULLRESET
		효과추가종류 = 3
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드全体",場所,"묘지",200)
	ENDIF
ENDIF



@CARDNAME_150598,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 우차치미-메
	CASE "略称"
RESULTS = 사안신 토큰
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_150598(参照先)
;名前専用
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
ENDSELECT

@CARD_EXPLANATION_150598(종류)
#DIM DYNAMIC 종류

PRINTL 「궁극환신 알티미틀 비시바-르킨」의 효과로 생성된다.
PRINTL 이 토큰은 소재로 할 수 없다.

@CARDCAN_150598(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,150598)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_150598(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード


IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"不材")
ENDIF
