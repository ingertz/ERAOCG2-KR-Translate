@CARDNAME_50597,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 궁극신 알티마야 촐킨
	CASE "略称"
RESULTS = 촐킨
	CASE "カテゴリ"
RESULTS:0 = 궁극신
ENDSELECT


@CARD_50597(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "특수불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARDSUMMON_50597(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_50597(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 S 소환할 수 없으며, 자신 필드의 앞면 표시의 레벨 5 이상으로 같은 레벨의,
PRINTL 튜너와 튜너 이외의 몬스터를 1체씩 묘지로 보냈을 경우에만 특수 소환할 수 있다.
CALL CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ①: 자신 / 상대 턴에 1번, 발동할 수 있다. "듀얼룡" 몬스터 1장을 엑스트라 덱에서 특수 소환한다.
CALL CARD_ABLE(0,지속효과)
PRINTL ②: 자신 필드에 "듀얼룡" 몬스터가 존재할 경우, 이 카드는 공격 회피와 대상 내성을 얻는다.
CALL CARD_ABLE(0,攻撃時強制誘発)
PRINTL ③: 자신 필드에 "듀얼룡" 몬스터가 존재하지 않고, 이 카드가 몬스터와 전투할 경우,
PRINTL 이 카드의 공격력은 그 상대 몬스터의 공격력과 같아진다.

@CARDCAN_50597(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 条件レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50597)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,50597)
	SIF RESULT == 1
		RETURN 0
	;参考先：ラティス
	FOR カウンタ,0,5
		SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
			CONTINUE
		TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:カウンタ")},"종류"
		SIF GROUPMATCH(RESULT,엑시즈,링크)
			CONTINUE
		SIF GETVAR(@"%듀얼리스트%_MZ_Ｔ:カウンタ") >= 1
			CONTINUE
		SIF GETVAR(@"%듀얼리스트%_MZ_星:カウンタ") < 5
			CONTINUE
		条件レベル = GETVAR(@"%듀얼리스트%_MZ_星:カウンタ")
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件튜너 = 1
		효과条件レベル = 条件レベル
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件달성++
	NEXT
	SIF 条件달성 == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1	
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 듀얼룡
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 듀얼룡
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_50597(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択レベル
#DIM DYNAMIC 対戦カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50597)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件튜너 = -1
		CALLFORM 효과条件レベル_ADV(5, "以上")
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択レベル = GETVAR(@"%듀얼리스트%_MZ_星:選択カード")
			CALL FULLRESET
			효과条件튜너 = 1
			효과条件レベル = 選択レベル
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과条件튜너 = -1
				효과条件レベル = 選択レベル
				CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
				IF RESULT == 1
					CALL FULLRESET
					효과条件特不無視 = 1
					CALL CARD_EX_SUMMON(듀얼리스트, "EX덱", 場所)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50597,10)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件カテゴリ = 듀얼룡
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 지속효과
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"対耐")
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻避")
ELSEIF 종류 == 攻撃時強制誘発
	対戦カード = 戦闘場所(듀얼리스트,1)
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一時",GETVAR(@"%対面者%_MZ_攻:対戦カード"))
ENDIF

