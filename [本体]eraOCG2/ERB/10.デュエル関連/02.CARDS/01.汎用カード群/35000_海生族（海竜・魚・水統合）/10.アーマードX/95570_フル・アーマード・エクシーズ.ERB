@CARDNAME_95570,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 풀 아머드 엑시즈
	CASE "略称"
RESULTS = FA 엑시즈
	CASE "カテゴリ"
RESULTS:0 = 아머드 엑시즈
RESULTS:1 = 엑시즈
ENDSELECT


@CARD_95570(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_95570(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,마함起動 ,フリー誘発)
PRINTL ①: 자신 필드에 X 몬스터가 존재할 경우 발동할 수 있다.
PRINTL "풀아머드" X 몬스터를 자신 X 몬스터 1체 위에 겹쳐 엑시즈 소환한다.

CALLFORM CARD_ABLE(0,묘지起動 ,フリー誘発)
PRINTL ②: 묘지의 이 카드를 제외하고, 자신 필드의 X 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 묘지의 X 몬스터 1장을 이하의 효과를 가지는 장착 마법 카드 취급으로 대상 몬스터에게 장착한다.
PRINTL ●장착 몬스터의 공격력은, 이 카드의 공격력만큼 올린다.
PRINTL ●장착 몬스터가 전투 / 효과로 파괴될 경우, 대신에 이 카드를 파괴한다.



@CARDCAN_95570(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,95570)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,0)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 풀아머드
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 엑시즈
	효과条件重ねてＸ = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	RETURN 1
ENDIF

@CARDEFFECT_95570(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードB
#DIMS DYNAMIC 対面者


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,95570)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;通常마법汎用処理
		CALL FULLRESET
		효과条件종류 = 엑시즈
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			효과条件カテゴリ = 풀아머드
			효과条件종류 = 엑시즈	
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カードB = RESULT
			IF 選択カードB != 9999
				CALL RESETCONDITION_MAT
				必要素材数 = 1
				重ねてＸ = 1
				素材条件固定 = 選択カード
				TRYCALLFORM XYZSUMMON(듀얼리스트,選択カードB)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,95570)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件종류 = 엑시즈
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종류 = 엑시즈
			CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,選択カード,(1 + 2 + 4),95570)
		ENDIF
	ENDIF
ENDIF