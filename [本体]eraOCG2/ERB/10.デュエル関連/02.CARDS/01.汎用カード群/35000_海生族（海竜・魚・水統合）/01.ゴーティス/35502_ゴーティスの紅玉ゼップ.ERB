@CARDNAME_35502,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 고티스의 홍옥 제프
	CASE "略称"
RESULTS = 제프
	CASE "カテゴリ"
RESULTS:0 = 고티스
ENDSELECT

@CARD_35502(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 바다생물족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 1000
	CASE "튜너"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_35502(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,손패起動)
PRINTL ①: 패의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신의 묘지의 바다 생물족 몬스터 1장을 대상으로 하고, 그 몬스터를 제외한다.
CALL CARD_ABLE(2,제외時)
PRINTL ②: 이 카드가 상대 턴에 제외되었을 경우에 발동할 수 있다. 이 카드를 특수 소환한다.
CALL CARD_ABLE(3,필드起動)
PRINTL ③: 이 카드가 특수 소환되어 있을 경우, 상대 턴의 메인 페이즈에 발동할 수 있다.
PRINTL 이 카드명을 포함하는 자신 필드의 몬스터를 소재로 하여 바다 생물족 S 몬스터 1장의 S 소환을 실행한다.

@CARDCAN_35502(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,35502)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	SIF 발동済_1(듀얼리스트,35502)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 바다생물족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 제외時
	SIF 발동済_2(듀얼리스트,35502)
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,35502,"제외")
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 특수소환時
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_3(듀얼리스트,35502)
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 싱크로
	효과条件종족 = 바다생물족
	強制素材場所 = 지정場所(場所,ゾーン)
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_35502(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35502,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き)
		CALL FULLRESET
		효과条件종족 = 바다생물족
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
	ENDIF

ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35502,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲제외 = 1
효과条件名前 = 고티스의 홍옥 제프
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 특수소환時
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",1,1)
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35502,3)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = 싱크로
		효과条件종족 = 바다생물족
		強制素材場所 = 지정場所(場所,ゾーン)
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL SYNSUMMON(듀얼리스트, 選択カード)
		ENDIF
	ENDIF
ENDIF

