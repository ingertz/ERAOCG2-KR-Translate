@CARDNAME_51561,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 정크 스피더
	CASE "略称"
RESULTS = JUNK 스피더
	CASE "カテゴリ"
RESULTS:0 = 정크

ENDSELECT


@CARD_51561(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 1000

	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARDSUMMON_51561(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 5
IF EX参照 == 0 || EX参照 == 1
	素材条件カテゴリ = 싱크론
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,51561) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_51561(종류)
#DIM DYNAMIC 종류

PRINTL 「싱크론」튜너＋튜너 이외의 몬스터 1체 이상
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,EX소환時)
PRINTL ①: 이 카드가 S 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 덱에서 「싱크론」튜너를 가능한 한 수비 표시로 특수 소환한다 (같은 레벨은 1체까지).
PRINTL 이 효과를 발동하는 턴, 자신은 S 몬스터밖에 EX덱에서 특수 소환할 수 없다.
CALLFORM CARD_ABLE(2,攻撃時誘発)
PRINTL ②: 이 카드가 몬스터와 전투를 실행하는 공격 선언 시에 발동할 수 있다.
PRINTL 이 카드의 공격력은 턴 종료 시까지 원래 공격력만큼 올라간다.


@CARDCAN_51561(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,51561)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,51561)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 싱크론
	효과条件튜너 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 싱크로以外
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX이력")
	SIF RESULT != 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,51561)
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_51561(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM 選択カード


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51561,1)
	IF RESULT == 0
		FOR カウンタ,1,6
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 싱크론
			효과条件튜너 = 1
			효과条件レベル = カウンタ
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			SIF モンスター数(듀얼리스트) >= 5
				BREAK
		NEXT
		EX소환可能종류 = 싱크로
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51561,2)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",1800)
	ENDIF
ENDIF
