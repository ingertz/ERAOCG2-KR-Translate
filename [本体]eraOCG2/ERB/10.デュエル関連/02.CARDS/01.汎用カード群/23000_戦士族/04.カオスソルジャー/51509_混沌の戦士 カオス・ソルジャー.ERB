@CARDNAME_51509,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 혼돈의 전사 카오스 솔저
	CASE "略称"
RESULTS = 혼돈의 전사
	CASE "カテゴリ"
RESULTS:0 = 카오스 솔저
RESULTS:1 = 카오스
ENDSELECT


@CARD_51509(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2500
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0
@CARDSUMMON_51509(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要링크数 = 3
素材条件固名前起動 = 1

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,51509) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_51509(종류)
#DIM DYNAMIC 종류

PRINTL 카드명이 다른 몬스터 2장 이상
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 레벨 7 이상의 몬스터가 자신의 묘지에 존재하는 상태에서 L소환한 이 카드는 대상 내성과 파괴 내성을 가진다.
CALLFORM CARD_ABLE(0,攻撃後誘発)
PRINTL ②: 이 카드가 전투로 상대 몬스터를 파괴했을 때, 이하의 효과 중에서 1개를 선택하여 발동할 수 있다.
PRINTL ● 이 카드의 공격력은 1500 올린다.
PRINTL ● 이 카드는 다음 자신 턴의 배틀 페이즈 중에 2번 공격할 수 있다.
PRINTL ● 필드의 카드 1장을 제외한다.


@CARDCAN_51509(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,51509)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時強制
	CALL FULLRESET
	효과範囲묘지 = 1
	CALLFORM 효과条件レベル_ADV(7, "以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン開始時予約
	RETURN 1
ELSEIF 종류 == ターン終了時予約
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_51509(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択효과
#DIM DYNAMIC 基準点

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時強制
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",2,1)
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"対耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"破耐")
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51509)
	IF RESULT == 0
		IF 듀얼리스트 == "자신"
PRINTL ● 이 카드의 공격력은 1500 올린다.
PRINTL ● 이 카드는 다음 자신 턴의 배틀 페이즈 중에 2번 공격할 수 있다.
PRINTL ● 필드의 카드 1장을 제외한다.
PRINTL 효과를 사용하지 않는다.
			BINPUT
			選択효과 = RESULT
		ELSEIF 듀얼리스트 == "상대"
			基準点 = GETVAR(@"%듀얼리스트%_MZ_攻:場所") + 1
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과条件以攻撃力 = 基準点 + 1500
			효과条件除去種 = 제외
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				選択효과 = 2
			ELSE
				CALL FULLRESET
				효과範囲상대필드 = 1
				효과条件以攻撃力 = 基準点
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				IF RESULT == 1
					選択효과 = 0
				ELSE
					CALL FULLRESET
					효과範囲상대필드 = 1
					효과条件CNT = 1
					CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
					IF RESULT > 1
						選択효과 = 1
					ELSE
						CALL FULLRESET
						효과範囲상대필드 = 1
						효과範囲상대마함 = 1
						효과条件除去種 = 제외
						CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
						IF RESULT == 1
							選択효과 = 2
						ELSE
							選択효과 = 1
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
		IF 選択효과 == 0
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",1500)
		ELSEIF 選択효과 == 1
			CALL CARD_RESERV(듀얼리스트,51509,スタン予約)
		ELSEIF 選択효과 == 2
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"제외")
		ENDIF
	ENDIF
ELSEIF 종류 == ターン開始時予約
	IF ターンプレイヤー == 듀얼리스트
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"戦",1)
	ELSE
		CALL CARD_RESERV(듀얼리스트,51509)
	ENDIF
ELSEIF 종류 == ターン終了時予約
	CALL CARD_RESERV(듀얼리스트,51509,スタン予約)
ENDIF
