@CARDNAME_25515,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 합무신기－후츠카가미
	CASE "略称"
RESULTS = 합무신기이경
	CASE "カテゴリ"
RESULTS:0 = 무신기
RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_25515(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1100
	CASE "守備力"
		RETURN 2100
ENDSELECT

RETURN 0
@CARD_EXPLANATION_25515(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 「합무신기－후츠카가미」 이외의 「무신」 또는 「무신기」 몬스터가 존재할 경우에 발동할 수 있다.
PRINTL 패의 이 카드를 특수 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 / 상대 턴에 묘지의 이 카드를 제외하고, 패에서 「무신」 또는 「무신기」 몬스터 1장을 묘지로 보내고 발동할 수 있다.
PRINTL 이 턴, 자신이 받는 모든 데미지는 0이 되고, 자신 필드의 「무신」 몬스터는 대상 내성을 얻는다.

@CARDCAN_25515(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,25515)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,25515)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ:0 = 무신
	효과条件カテゴリ:1 = 무신기
	효과条件固名前 = 합무신기－후츠카가미
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	SIF 발동済_2(듀얼리스트,25515)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ:0 = 무신
	효과条件カテゴリ:1 = 무신기	
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 短紋章起動

	RETURN 1
ENDIF

@CARDEFFECT_25515(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25515,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
	ENDIF

ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25515,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件カテゴリ:0 = 무신
		효과条件カテゴリ:1 = 무신기	
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL SET_PLAYER_INFO(듀얼리스트,"ダメージ無効",1)
			CALL SET_PLAYER_INFO(듀얼리스트,"효과ダメージ無効",1)
			;T1紋章（短紋章발동）
			CALL CARD_RESERV(듀얼리스트,25515,3)
		ENDIF
	ENDIF

ELSEIF 종류 == 短紋章起動
	CALL FULLRESET
	효과条件カテゴリ = 무신
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"対耐")
ENDIF

