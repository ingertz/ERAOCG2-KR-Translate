@CARDNAME_1202,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 사이버스 위키드
	CASE "略称"
RESULTS = 위키드
	CASE "カテゴリ"
RESULTS:0 = 사이버스
ENDSELECT


@CARD_1202(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 사이버스족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 800
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARDSUMMON_1202(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要링크数 = 2
素材条件종족 = 사이버스족

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,1202) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_1202(종류)
#DIM DYNAMIC 종류

PRINTL 사이버스족 몬스터 2장
PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,EX소환時)
PRINTL ①: 이 카드가 링크 소환했을 때 발동할 수 있다.
PRINTL 이 카드는 전투 / 파괴 내성을 얻는다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ②: 이 카드가 이미 몬스터 존에 존재하는 상태에서, 자신 필드에 효과 몬스터가 특수 소환되었을 경우,
PRINTL 자신의 묘지에서 사이버스족 몬스터 1장을 제외하고 발동할 수 있다.
PRINTL 덱에서 사이버스족 튜너 1장을 패에 더한다.
PRINTL 그 후, 이 효과 발동 시에 특수 소환된 자신의 사이버스족 몬스터는 파괴 내성을 얻는다.


@CARDCAN_1202(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1202)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時
	RETURN 1

ELSEIF 종류 == 효과後誘発
	SIF 발동済_2(듀얼리스트,1202)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_最召場所") == 場所
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 효과全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 사이버스족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 사이버스족
	효과条件튜너 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1202(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 효과先


IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1202)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"戦耐",2)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"破耐",2)
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1202,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족 = 사이버스족
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			효과条件튜너 = 1
			효과条件종족 = 사이버스족
			CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",효과先,"破耐",2)
		ENDIF	
	ENDIF
ENDIF


