@CARDNAME_71011,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 마그넷 트윈 세이버
	CASE "略称"
RESULTS = 전자기쌍검
	CASE "カテゴリ"
RESULTS:0 = 마그넷
ENDSELECT


@CARD_71011(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 装備마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71011(종류)
#DIM DYNAMIC 종류

PRINTL 자신 필드의 땅 속성 몬스터 1장에게 장착할 수 있다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①: 장착 몬스터의 공격력은 300 올린다.
PRINTL 장착 몬스터가 레벨 8 이상의 「마그넷 워리어」 몬스터일 경우, 장착 몬스터는 추가 공격을 얻는다.
CALLFORM CARD_ABLE(0,효과後誘発)
PRINTL ②: 이 카드가 묘지에 존재하는 상태에서, 자신 필드에 원소정령족 땅 속성 몬스터가 특수 소환되었을 경우에 발동할 수 있다.
PRINTL 이 카드를 자신 필드의 땅 속성 몬스터 1장에게 장착한다.

@CARDCAN_71011(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71011)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件속성 = 땅속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後強制誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%装備者%_필드:装備先") == -1
		RETURN 0
	TRYCALLFORM CARDNAME_{GETVAR(@"%装備者%_필드:装備先")}, "カテゴリ"
	SIF !MATCH(RESULTS, "마그넷 워리어")
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_星:装備先") < 8
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件종족 = 원소정령족
	효과条件속성 = 땅속성
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71011(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 装備者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71011)
	IF RESULT == 0
		;패에서 발동後、마법トラップ 존에 カード이동
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件속성 = 땅속성
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
	ENDIF
ELSEIF 종류 == 攻守変動
	;装備モンスターがいないときは自壊する	
	CALL STATCHANGE_BASIC(듀얼리스트,"装備",場所,300)

ELSEIF 종류 == 攻撃後強制誘発
	CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"効済",1)
	CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"戦",1)
	PRINTW 전자の力が、更なる攻撃を可能にする！

ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71011)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件속성 = 땅속성
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,"묘지")
	ENDIF
ENDIF

