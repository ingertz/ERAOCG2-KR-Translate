@CARDNAME_73601,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 융합 뮤트리아스
	CASE "略称"
RESULTS = 뮤트-융합
	CASE "カテゴリ"
RESULTS:0 = 뮤트리아
RESULTS:1 = 융합
ENDSELECT


@CARD_73601(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 通常마법
ENDSELECT
RETURN 0

@CARD_EXPLANATION_73601(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 카드는 1턴에 1장밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動 ,손패起動)
PRINTL ①: 자신의 패 / 필드에서,
PRINTL 「뮤트리아」 융합 몬스터 카드에 의해 정해진 융합 소재 몬스터를 제외하고,
PRINTL 그 융합 몬스터 1장을 엑스트라 덱에서 융합 소환한다.
PRINTL 이 턴에 상대가 카드 효과를 발동했을 경우,
PRINTL 자신의 덱 / 묘지의 몬스터도 융합 소재로 할 수 있다.
PRINTL 

@CARDCAN_73601(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIM DYNAMIC EX条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73601)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 발동済_1(듀얼리스트,73601)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF GETVAR(@"%対面者%_モン발동数") >= 1
		EX条件달성 = 1
	CALL RESETAREA_MAT
	IF EX条件달성 == 1
		素材範囲덱 = 1	
		素材範囲묘지 = 1	
	ENDIF
	素材範囲필드 = 1
	素材範囲손패 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件カテゴリ = 뮤트리아
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_73601(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
#DIM DYNAMIC EX条件달성
;융합소환
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73601,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)

		SIF GETVAR(@"%対面者%_モン발동数") >= 1
			EX条件달성 = 1
		;EX덱確認
		CALL RESETAREA_MAT
		IF EX条件달성 == 1
			素材範囲덱 = 1	
			素材範囲묘지 = 1	
		ENDIF	
		素材範囲필드 = 1
		素材範囲손패 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件カテゴリ = 뮤트리아
		효과条件소환可 = 1	
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード,제외行き)
		ENDIF
	ENDIF		
ENDIF
