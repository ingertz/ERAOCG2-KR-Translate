@CARDNAME_24141,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 앤틱 기어 동상
	CASE "略称"
RESULTS = 고대의 기계상
	CASE "カテゴリ"
RESULTS:0 = 앤틱 기어
ENDSELECT


@CARD_24141(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 500
	CASE "守備力"
		RETURN 800
ENDSELECT

RETURN 0

@CARD_EXPLANATION_24141(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의, ①의 방법에 의한 특수소환은 1턴에 1번밖에 할 수 없으며, ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 상대 필드의 몬스터 수가 자신 필드의 몬스터보다 많을 경우,
PRINTL 이 카드는 패에서 특수 소환할 수 있다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 이 카드를 릴리스하고 발동할 수 있다.
PRINTL "고대의 기계상"을 제외한, "앤틱 기어" 몬스터 1장을 패 / 덱에서 소환 조건을 무시하고 특수 소환한다.



@CARDCAN_24141(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24141)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,24141)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= モンスター数(対面者)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,24141,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,24141)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件カテゴリ = 앤틱 기어
	효과条件종류 = モン全般
	효과条件소환可 = 1
	효과条件特不無視 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_24141(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所



IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24141,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24141,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件固ID = 24141
		효과条件カテゴリ = 앤틱 기어
		효과条件特不無視 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

