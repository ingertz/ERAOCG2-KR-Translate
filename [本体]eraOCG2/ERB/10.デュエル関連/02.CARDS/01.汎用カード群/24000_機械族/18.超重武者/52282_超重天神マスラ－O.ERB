@CARDNAME_52282,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 초중천신 마수라-O
	CASE "略称"
RESULTS = 마수라O
	CASE "カテゴリ"
RESULTS:0 = 초중무사
ENDSELECT


@CARD_52282(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	;효과モン/通常モン/의식/융합/싱크로/엑시즈/링크/초차원
	CASE "종류"
		RETURN 싱크로
	;어둠속성/빛속성/땅속성/물속성/화염속성/바람속성
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 12
	CASE "攻撃力"
		RETURN 2100
	CASE "守備力"
		RETURN 4000
ENDSELECT
RETURN 0


@CARDSUMMON_52282(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要レベル数 = 12
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,52282) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_52282(종류)
#DIM DYNAMIC 종류

PRINTL 튜너 ＋ 튜너 이외의 몬스터 1장 이상
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 이 카드는 앞면 수비 표시 상태로 공격할 수 있다.
PRINTL 그 경우, 이 카드는 수비력을 공격력으로 취급하여 데미지 계산을 실행한다.
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ②: 자신 필드의 「초중무사」 몬스터가 전투 / 효과로 파괴될 경우,
PRINTL 대신 자신 필드의 「초중무사」 카드 1장을 파괴할 수 있다.
CALLFORM CARD_ABLE(10,효과後誘発)
PRINTL ③: 1턴에 1번, 상대가 마법 / 함정 카드의 효과를 발동했을 경우에 발동할 수 있다.
PRINTL 자신은 손패가 3장이 되도록 덱에서 드로우한다.



@CARDCAN_52282(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,52282)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0	
	TRYCALLFORM CARDNAME_{破壊대상ID}("カテゴリ")
	SIF !MATCH(RESULTS,"초중무사")
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 초중무사
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後誘発
    SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 마함全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동",1)
	SIF RESULT != 1
		RETURN 0
	SIF 손패数(듀얼리스트) >= 3
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1

ENDIF

@CARDEFFECT_52282(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC ドロー枚数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"守戦")

ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52282)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件カテゴリ = 초중무사
		효과条件行先플래그 = 破壊行き
		효과条件他所 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF

ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52282,10)
	IF RESULT == 0
		ドロー枚数 = 3 - 손패数(듀얼리스트)
		CALL CARDDRAW_BASIC(듀얼리스트,ドロー枚数)
	ENDIF	

ENDIF
