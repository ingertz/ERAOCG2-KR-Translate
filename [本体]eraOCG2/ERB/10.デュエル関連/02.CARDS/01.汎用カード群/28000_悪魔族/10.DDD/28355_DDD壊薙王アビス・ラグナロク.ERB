@CARDNAME_28355,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = DDD 섬멸왕 심연 라그나로크
	CASE "略称"
RESULTS = DDD 라그나로크
	CASE "カテゴリ"
RESULTS:0 = DD
RESULTS:1 = DDD
ENDSELECT


@CARD_28355(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2200
	CASE "守備力"
		RETURN 3000
	CASE "分割"
		RETURN 펜듈럼
ENDSELECT
RETURN 0

@CARD_EXPLANATION_28355(종류)
#DIM DYNAMIC 종류


CALL TEXT_DECORATION("P_MAGIC")
PRINTL 이 카드명의 ①②의 P 효과는, 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ①: 자신의 메인 페이즈에 발동할 수 있다.
PRINTL 패 / EX덱에서 EX 이외의 「DD」 몬스터 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ②: 자신이 「DD」 몬스터를 특수 소환했을 경우, 자신의 묘지의 「DD」 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환하고, 자신은 1000 데미지를 받는다.
CALL TEXT_DECORATION("P_MAGIC_2")
PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(3,소환時,특수소환時)
PRINTL ①: 이 카드가 소환 / 특수 소환했을 경우, 자신의 묘지의 「DDD」 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ②: 1턴에 1번, 자신 필드의 다른 「DD」 몬스터 1장을 릴리스하고, 상대 필드의 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 제외한다.


@CARDSUMMON_AA_28355


@CARDCAN_28355(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,28355)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF 발동済_1(듀얼리스트,28355)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲EX덱 = 1
	효과条件カテゴリ = DD
	효과条件종류 = EX以外全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_2(듀얼리스트,28355)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0

	CALL FULLRESET
	효과条件カテゴリ = DD
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = DD
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,28355)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_3(듀얼리스트,28355)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = DDD
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,28355)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = DD
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,28355)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_28355(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択횟수
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28355)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28355,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲EX덱 = 1
		효과条件カテゴリ = DD
		효과条件종류 = EX以外全般
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28355,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = DD
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",1000)
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28355,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = DDD
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28355,10)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = DD
		효과条件他所 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"제외")
		ENDIF
	ENDIF
ENDIF



