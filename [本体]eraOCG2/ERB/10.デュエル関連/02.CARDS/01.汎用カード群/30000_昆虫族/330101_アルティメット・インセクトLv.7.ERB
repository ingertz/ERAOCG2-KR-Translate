@CARDNAME_330101,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 얼티밋 인섹트 Lv.7
	CASE "略称"
RESULTS = 궁극곤충Lv.7
	CASE "カテゴリ"
RESULTS:0 = Lv
RESULTS:1 = 얼티밋 인섹트
ENDSELECT


@CARD_330101(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 곤충족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2600
	CASE "守備力"
		RETURN 1200
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_330101(종류)
#DIM DYNAMIC 종류

PRINTL 「얼티밋 인섹트」는 1턴에 1번만 성장할 수 없다.
CALLFORM CARD_ABLE(1,ターン開始時)
PRINTL ①: 자신 스탠바이 페이즈에 발동할 수 있다.
PRINTL 이 카드를 묘지로 보내고, 「얼티밋 인섹트 Lv. OVER」 1장을 게임 밖에서 특수 소환한다.
PRINTL 「얼티밋 인섹트」는 1턴에 1번만 성장할 수 없다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②: 이 카드가 필드 위에 존재하는 한,
PRINTL 상대 몬스터의 공격력 수비력은 700 감소한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드는 몬스터 내성을 가진다.
CALLFORM CARD_ABLE(0,발동誘発)
PRINTL ④: 상대가 몬스터 효과를 발동했을 때 발동할 수 있다.
PRINTL 그 발동을 무효로 하고 파괴한다.


@CARDCAN_330101(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,330101)
SIF RESULT == 1
	RETURN 0

IF 종류 == ターン開始時
	SIF 발동済_1(듀얼리스트,130101)
		RETURN 0
	SIF 발동済_1(듀얼리스트,230101)
		RETURN 0    	
    SIF 발동済_1(듀얼리스트,330101)
		RETURN 0
	SIF 발동済_1(듀얼리스트,430101)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 3
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 3
		RETURN 0
	RETURN 1

ELSEIF 종류 == 발동誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_330101(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,330101,1)
	IF RESULT == 0
		CALL CARD_RECONSTRUCT(듀얼리스트,"필드",場所,430101)
	ENDIF

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"상대필드",場所,-700,-700,330101)	

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"モ耐")

ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,330101)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"묘지")
		RETURN 1
	ENDIF
	RETURN 0
ENDIF